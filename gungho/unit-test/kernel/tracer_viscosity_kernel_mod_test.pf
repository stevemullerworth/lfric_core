!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module tracer_viscosity_kernel_mod_test

  use constants_mod, only: i_def, r_def
  use pFUnit_Mod
  use yaxt,          only: xt_initialize, xt_finalize
  use mpi_mod,       only: store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: tracer_viscosity_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type tracer_viscosity_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod, only: feign_mixing_config
    use mixing_config_mod,         only : method_3d_smag

    implicit none

    class(tracer_viscosity_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_mixing_config( smagorinsky   = .false.,    &
                              viscosity     = .false.,    &
                              viscosity_mu  = 10.0_r_def, &
                              mix_factor    = 0.0_r_def,  &
                              method        = method_3d_smag )
  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration

    implicit none

    class(tracer_viscosity_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_all( this )

    use tracer_viscosity_kernel_mod, only: tracer_viscosity_code

    use get_unit_test_m3x3_dofmap_mod,       only: get_w0_m3x3_dofmap,      &
                                                   get_wtheta_m3x3_dofmap,  &
                                                   get_m3x3_stencil_dofmap_cross
    use get_unit_test_m3x3_q3x3x3_sizes_mod, only: get_w0_m3x3_q3x3x3_size, &
                                                   get_wtheta_m3x3_q3x3x3_size
    use get_unit_test_3x3x3_chi_mod,         only : get_w0_3x3x3_field
    implicit none

    class(tracer_viscosity_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    real(r_def)    :: answer
    integer(i_def) :: cell, i, j, k

    ! Variables for testing tracer_viscosity
    real(r_def), allocatable :: t_inc_data(:)
    real(r_def), allocatable :: t_data(:)

    integer(i_def) :: ndf_chi, undf_chi
    integer(i_def) :: ndf_t, undf_t
    integer(i_def) :: stencil_t_size

    integer(i_def), allocatable :: map_chi(:,:)
    integer(i_def), allocatable :: map_t(:,:)
    integer(i_def), allocatable :: stencil_map_t(:,:,:)

    integer(i_def) :: ndf_w0, undf_w0
    integer(i_def) :: ndf_wtheta, undf_wtheta
    integer(i_def) :: stencil_cross_size

    integer(i_def) :: nlayers, ncells
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    real(r_def), allocatable :: chi_data(:,:)

    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_wtheta(:,:)
    integer(i_def), allocatable :: stencil_cross_wtheta(:,:,:)

    !=====================================
    ! Get infrastructure support data
    !=====================================
    nlayers = 3

    ! Get sizes
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells,         &
                                  dim_space, dim_space_diff,       &
                                  nqp_h, nqp_v, nlayers )
    call get_wtheta_m3x3_q3x3x3_size(                              &
                                  ndf_wtheta, undf_wtheta, ncells, &
                                  dim_space, dim_space_diff,       &
                                  nqp_h, nqp_v, nlayers )
    ! Get maps
    call get_w0_m3x3_dofmap( map_w0 )
    call get_wtheta_m3x3_dofmap( map_wtheta )
    call get_m3x3_stencil_dofmap_cross( stencil_cross_wtheta, map_wtheta )
    stencil_cross_size = 5

    ! Compute coordinates
    allocate(chi_data(3,undf_w0))

    call get_w0_3x3x3_field(chi_data(1,:), chi_data(2,:), chi_data(3,:), &
                            dx, dy, dz, map_w0, nlayers)

    !=====================================
    ! Assign data for testing
    !=====================================
    ! Assign chi
    ndf_chi  = ndf_w0
    undf_chi = undf_w0
    allocate( map_chi, source=map_w0 )

    ! Assign tracer
    ndf_t  = ndf_wtheta
    undf_t = undf_wtheta
    allocate( map_t, source=map_wtheta )
    allocate( stencil_map_t, source=stencil_cross_wtheta )
    stencil_t_size = stencil_cross_size

    ! Assign data
    allocate(t_data(undf_t))
    allocate(t_inc_data(undf_t))
    t_data     = 300.0_r_def
    t_inc_data = 0.0_r_def

    !=====================================
    ! Apply test on interior cell 5
    !=====================================
    cell=5
    call tracer_viscosity_code( nlayers,                 &
                                t_inc_data,              &
                                t_data,                  &
                                stencil_t_size,          &
                                stencil_map_t(:,:,cell), &
                                chi_data(1,:),           &
                                chi_data(2,:),           &
                                chi_data(3,:),           &
                                ndf_t,                   &
                                undf_t,                  &
                                stencil_map_t(:,1,cell), &
                                ndf_chi,                 &
                                undf_chi,                &
                                map_chi(:,cell) )

     answer = 0.0_r_def
     @assertEqual(answer, t_inc_data(stencil_map_t(1,1,cell) ), tol)

     deallocate( map_chi )
     deallocate( map_t )
     deallocate( t_data )
     deallocate( t_inc_data )
     deallocate( stencil_map_t )

     deallocate( chi_data )
     deallocate( map_w0 )
     deallocate( map_wtheta )
     deallocate( stencil_cross_wtheta )

  end subroutine test_all

end module tracer_viscosity_kernel_mod_test
