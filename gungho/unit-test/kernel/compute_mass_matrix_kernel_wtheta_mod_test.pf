!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the Wtheta mass matrix computation
!>
module compute_mass_matrix_kernel_wtheta_mod_test

  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size,  &
                                                  get_wtheta_m3x3_q3x3x3_size
  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap
  use get_unit_test_q3x3x3_basis_mod,      only : get_w0_q3x3x3_diff_basis, &
                                                  get_wtheta_q3x3x3_basis
  use get_unit_test_q3x3x3_quadrature_mod, only : get_gaussian_q3x3x3_quadrature_weights_xy, &
                                                  get_gaussian_q3x3x3_quadrature_weights_z
  use get_unit_test_3x3x3_chi_mod,         only : get_w0_3x3x3_field
  use pFUnit_Mod
  use yaxt,                                only : xt_initialize, xt_finalize
  use mpi_mod,                             only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: compute_mass_matrix_wtheta_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_mass_matrix_wtheta_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : cellshape_quadrilateral
    use feign_config_mod,          only : feign_finite_element_config

    implicit none

    class(compute_mass_matrix_wtheta_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(           &
             cellshape=cellshape_quadrilateral, &
             coordinate_order=0_i_def,          &
             element_order=0_i_def,             &
             rehabilitate=.true.,               &
             vorticity_in_w1=.false. )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(compute_mass_matrix_wtheta_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_mass_matrix_kernel_wtheta_mod, only : compute_mass_matrix_wtheta_code

    implicit none

    class(compute_mass_matrix_wtheta_test_type), intent(inout) :: this

    real(kind=r_def) :: answer
    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    integer(i_def) :: nlayers, ncells, ncell_3d, cell, icell
    integer(i_def) :: ndf_w0, undf_w0, ndf_wtheta, undf_wtheta
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def) :: i, j, k

    integer(i_def), allocatable :: map_w0(:,:)
    real(r_def), allocatable :: diff_basis_w0(:,:,:,:), &
                                basis_wtheta(:,:,:,:)
    real(r_def), allocatable :: wh(:), wv(:)
    real(r_def), allocatable :: chi1(:), chi2(:), chi3(:)
    real(r_def), allocatable :: local_stencil(:,:,:)

    nlayers = 3
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, ncells, &
                                      dim_space, dim_space_diff, &
                                      nqp_h, nqp_v, &
                                      nlayers )
    call get_w0_m3x3_dofmap(map_w0)
    call get_w0_q3x3x3_diff_basis(diff_basis_w0)
    call get_wtheta_q3x3x3_basis(basis_wtheta)
    call get_gaussian_q3x3x3_quadrature_weights_xy(wh)
    call get_gaussian_q3x3x3_quadrature_weights_z(wv)

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))

    call get_w0_3x3x3_field(chi1, chi2, chi3, dx, dy, dz, &
                            map_w0, nlayers)

    ncell_3d = ncells*nlayers

    allocate(local_stencil(ndf_wtheta, ndf_wtheta, ncell_3d))
    local_stencil(:,:,:) = 0.0_r_def

    cell = 1
    call compute_mass_matrix_wtheta_code( cell,                      &
                                          nlayers,                   &
                                          ncell_3d,                  &
                                          local_stencil,             &
                                          chi1,                      &
                                          chi2,                      &
                                          chi3,                      &
                                          ndf_wtheta,                &
                                          basis_wtheta,              &
                                          ndf_w0,                    &
                                          undf_w0,                   &
                                          map_w0(:,cell),            &
                                          diff_basis_w0,             &
                                          nqp_h,                     &
                                          nqp_v,                     &
                                          wh,                        &
                                          wv                         &
                                        )

    answer = (1.0_r_def/3.0_r_def)
    @assertEqual(answer, local_stencil(1, 1, 1 )/(dx*dy*dz), tol)
    answer = (1.0_r_def/6.0_r_def)
    @assertEqual(answer, local_stencil(1, 2, 1 )/(dx*dy*dz), tol)

    answer = (1.0_r_def/3.0_r_def)
    @assertEqual(answer, local_stencil(1, 1, 2 )/(dx*dy*dz), tol)
    answer = (1.0_r_def/6.0_r_def)
    @assertEqual(answer, local_stencil(1, 2, 2 )/(dx*dy*dz), tol)

    deallocate(map_w0)
    deallocate(diff_basis_w0)
    deallocate(basis_wtheta)
    deallocate(wh)
    deallocate(wv)
    deallocate(chi1)
    deallocate(chi2)
    deallocate(chi3)
    deallocate(local_stencil)

  end subroutine test_all

end module compute_mass_matrix_kernel_wtheta_mod_test
