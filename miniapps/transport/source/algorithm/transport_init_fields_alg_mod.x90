!-----------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!-----------------------------------------------------------------------------
!> @brief Initialisation of density field for the transport miniapp.
!> @details The initial density field can be generated either by sampling
!>          or by projection.
!-----------------------------------------------------------------------------

module transport_init_fields_alg_mod

  use constants_mod,                     only: r_def, i_def
  use geometric_constants_mod,           only: get_coordinates_xyz, &
                                               get_coordinates,     &
                                               get_panel_id
  use finite_element_config_mod,         only: element_order
  use field_mod,                         only: field_type
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type
  use initial_rho_sample_kernel_mod,     only: initial_rho_sample_kernel_type
  use set_rho_kernel_mod,                only: set_rho_kernel_type
  use initial_pressure_config_mod,       only: method, method_sampled

  implicit none

  private

  public :: transport_init_fields_alg

contains
!-----------------------------------------------------------------------------
!> @brief Initialisation of density and theta fields
!>        for the transport miniapp.
!> @param[in,out] wind_n  3D initial wind
!> @param[in,out] density Density field
!> @param[in,out] theta   theta/tracer field
!> @param[in,out] dep_pts_x X-coordinates of departure points
!> @param[in,out] dep_pts_y Y-coordinates of departure points
!> @param[in,out] dep_pts_z Z-coordinates of departure points
!> @param[in,out] divergence Divergence of the initial wind
!-----------------------------------------------------------------------------

subroutine transport_init_fields_alg( wind_n,    &
                                      density,   &
                                      theta,     &
                                      dep_pts_x, &
                                      dep_pts_y, &
                                      dep_pts_z, &
                                      divergence )

    implicit none

    type(field_type), intent(inout)     :: wind_n
    type(field_type), intent(inout)     :: density
    type(field_type), intent(inout)     :: theta
    type(field_type), intent(inout)     :: dep_pts_x
    type(field_type), intent(inout)     :: dep_pts_y
    type(field_type), intent(inout)     :: dep_pts_z
    type(field_type), intent(inout)     :: divergence

    type(quadrature_xyoz_type)          :: qr
    type(quadrature_rule_gaussian_type) :: quadrature_rule
    type(field_type), pointer           :: chi_xyz(:) => null()
    type(field_type), pointer           :: chi_sph(:) => null()
    type(field_type), pointer           :: panel_id => null()
    real(kind=r_def), parameter         :: initial_time = 0.0_r_def
    integer(kind=i_def)                 :: mesh_id

    mesh_id = wind_n%get_mesh_id()

    chi_xyz  => get_coordinates_xyz(mesh_id)
    chi_sph  => get_coordinates(mesh_id)
    panel_id => get_panel_id(mesh_id)
    qr = quadrature_xyoz_type( element_order+3, quadrature_rule )

    call invoke( setval_c( wind_n, 1.0_r_def ),    &
                 setval_c( dep_pts_x, 1.0_r_def ), &
                 setval_c( dep_pts_y, 1.0_r_def ), &
                 setval_c( dep_pts_z, 1.0_r_def ), &
                 setval_c( divergence, 1.0_r_def) )

    if ( method == method_sampled ) then
      call invoke( initial_rho_sample_kernel_type( density, chi_xyz, initial_time ), &
                   initial_rho_sample_kernel_type( theta  , chi_xyz, initial_time )  )
     else
      call invoke( set_rho_kernel_type( density, chi_sph, panel_id, initial_time, qr ), &
                   set_rho_kernel_type( theta  , chi_sph, panel_id, initial_time, qr )  )
    end if

  end subroutine transport_init_fields_alg

end module transport_init_fields_alg_mod
