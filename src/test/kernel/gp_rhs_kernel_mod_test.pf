!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------
module gp_rhs_kernel_mod_test
  implicit none 

contains

  @test
  subroutine gp_rhs_kernel_test()
    use pFUnit_Mod
    use gp_rhs_kernel_mod,        only : gp_rhs_code
    use quadrature_mod,  only : quadrature_type, QR3
    use constants_mod,            only : r_def
    implicit none

    integer :: num_layers, undf
    type(quadrature_type), pointer  :: qr => null()
    real(kind=r_def) :: f_data(1), rhs(8)
    real(kind=r_def) :: answer, tol
    real(kind=r_def), allocatable :: basis(:,:,:,:), &
                                     basis_f(:,:,:,:), &
                                     diff_basis(:,:,:,:)
    integer :: ndf_w0, map_w0(8), ndf_f, map_f(1), undf_f
    real(kind=r_def) :: chi(8,3)
    real(kind=r_def) :: x_vert(8,3)
    real(kind=r_def),pointer :: xqp_h(:,:), xqp_v(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    integer          :: nqp_h, nqp_v
    real(kind=r_def) :: fx, fy, fz
    integer :: df, dim, qp1, qp2

    qr => qr%get_instance(QR3,9,3)

    xqp_h => qr%get_xqp_h()
    xqp_v => qr%get_xqp_v()
    wh    => qr%get_wqp_h()
    wv    => qr%get_wqp_v()
    nqp_h = qr%get_nqp_h()
    nqp_v = qr%get_nqp_v()

    x_vert(1,:) = (/ 0.0_r_def, 0.0_r_def, 0.0_r_def /)
    x_vert(2,:) = (/ 1.0_r_def, 0.0_r_def, 0.0_r_def /)
    x_vert(3,:) = (/ 1.0_r_def, 1.0_r_def, 0.0_r_def /)
    x_vert(4,:) = (/ 0.0_r_def, 1.0_r_def, 0.0_r_def /)
    x_vert(5,:) = (/ 0.0_r_def, 0.0_r_def, 1.0_r_def /)
    x_vert(6,:) = (/ 1.0_r_def, 0.0_r_def, 1.0_r_def /)
    x_vert(7,:) = (/ 1.0_r_def, 1.0_r_def, 1.0_r_def /)
    x_vert(8,:) = (/ 0.0_r_def, 1.0_r_def, 1.0_r_def /)    

    ndf_w0 = 8
    undf=8
    undf_f = 8
    ndf_f = 1
    allocate( basis_f   (1,ndf_f,nqp_h,nqp_v), &
              basis     (1,ndf_w0,nqp_h,nqp_v), &
              diff_basis(3,ndf_w0,nqp_h,nqp_v) )


    do df = 1,ndf_w0
      map_w0(df) = df
      do qp2 = 1,nqp_v
        do qp1 = 1,nqp_h
          fx = (1.0_r_def - x_vert(df,1)) + (-1.0_r_def)**(int(x_vert(df,1))+1)*xqp_h(qp1,1)
          fy = (1.0_r_def - x_vert(df,2)) + (-1.0_r_def)**(int(x_vert(df,2))+1)*xqp_h(qp1,2)
          fz = (1.0_r_def - x_vert(df,3)) + (-1.0_r_def)**(int(x_vert(df,3))+1)*xqp_v(qp2)
          diff_basis(1,df,qp1,qp2) = fy*fz
          diff_basis(2,df,qp1,qp2) = fx*fz
          diff_basis(3,df,qp1,qp2) = fx*fy
          basis(1,df,qp1,qp2) = fx*fy*fz
        end do
      end do
      do dim = 1,3
        if ( x_vert(df,dim) < 1.0_r_def ) &
          diff_basis(dim,df,:,:) = -diff_basis(dim,df,:,:)     
      end do
    end do
    chi(:,:) = x_vert(:,:)

    map_f(1) = 1
    basis_f(:,:,:,:) = 1.0_r_def
    f_data(1) = 5.0_r_def

    num_layers=1
    rhs(:) = 0.0_r_def
    call gp_rhs_code(num_layers,                       &
                     rhs, f_data,                      &
                     chi(:,1), chi(:,2), chi(:,3),     &
                     ndf_w0, undf, map_w0, basis,      &
                     ndf_f, undf_f,map_f, basis_f,     &
                     ndf_w0, undf, map_w0, diff_basis, &
                     nqp_h, nqp_v, wh, wv )

! Answer for hardwired jacobian
!    answer = ( 6000.0_r_def * 1000.0_r_def * 2000.0_r_def )*0.125_r_def*f_data(1)

    answer = ( 1.0_r_def * 1.0_r_def * 1.0_r_def )*0.125_r_def*f_data(1) 
    tol    = 1.0e-2_r_def
    do df = 1, ndf_w0
      @assertEqual(answer, rhs(df), tol)
      @assertEqual(answer, answer, tol)
    end do

  end subroutine gp_rhs_kernel_test

end module gp_rhs_kernel_mod_test
