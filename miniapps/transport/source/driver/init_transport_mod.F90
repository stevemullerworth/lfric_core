!-----------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Initialises functionality for transport.

!> @details Handles initialisation of wind, density and chi fields.
!> transport_init_fields_alg() is used to initialise density.

module init_transport_mod

  use constants_mod,                  only: i_def
  use field_mod,                      only: field_type,               &
                                            write_interface
  use finite_element_config_mod,      only: element_order
  use fs_continuity_mod,              only: W2, W3
  use runtime_constants_mod,          only: create_runtime_constants
  use function_space_mod,             only: function_space_type
  use function_space_collection_mod,  only: function_space_collection
  use io_config_mod,                  only: write_diag, &
                                            use_xios_io
  use io_mod,                         only: xios_write_field_face
  use log_mod,                        only: log_event,                &
                                            LOG_LEVEL_INFO
  use transport_init_fields_alg_mod,  only: transport_init_fields_alg

  implicit none

  contains

  !> @param[in] mesh_id                Mesh-id
  !> @param[in] twod_mesh_id           2D Mesh-id
  !> @param[in,out] chi                Coordinate field
  !> @param[in] shifted_mesh_id        Mesh-id for vertically shifted coordinates
  !> @param[in,out] shifted_chi        Coordinate field for vertically shifted coordinates
  !> @param[in,out] wind_n             Wind field at timestep n
  !> @param[in,out] density            Density field
  !> @param[in,out] dep_pts_x          Departure points in the x-direction
  !> @param[in,out] dep_pts_y          Departure points in the y-direction
  !> @param[in,out] dep_pts_z          Departure points in the z-direction
  !> @param[in,out] increment          Density increment
  !> @param[in,out] divergence         Divergence field
  !> @param[in,out] wind_shifted       Wind field on vertically shifted W2 field
  !> @param[in,out] density_shifted    Density field on vertically shifted W3 field
  subroutine init_transport( mesh_id, twod_mesh_id, chi, shifted_mesh_id, shifted_chi, &
                             wind_n, density, dep_pts_x, dep_pts_y, dep_pts_z,         &
                             increment, divergence, wind_shifted, density_shifted )

    implicit none

    integer(i_def),   intent(in)      :: mesh_id
    integer(i_def),   intent(in)      :: twod_mesh_id
    type(field_type), intent(inout)   :: chi(:)
    integer(i_def),   intent(in)      :: shifted_mesh_id
    type(field_type), intent(inout)   :: shifted_chi(:)
    type(field_type), intent(inout)   :: wind_n
    type(field_type), intent(inout)   :: density
    type(field_type), intent(inout)   :: dep_pts_x
    type(field_type), intent(inout)   :: dep_pts_y
    type(field_type), intent(inout)   :: dep_pts_z
    type(field_type), intent(inout)   :: increment
    type(field_type), intent(inout)   :: divergence
    type(field_type), intent(inout)   :: wind_shifted
    type(field_type), intent(inout)   :: density_shifted

    type(function_space_type), pointer       :: function_space => null()
    procedure(write_interface), pointer :: tmp_write_ptr => null()

    wind_n   = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W2 ) )
    density = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W3 ) )
    increment = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W3 ) )
    divergence = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W3 ) )

    dep_pts_x  = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W2 ) )
    dep_pts_y  = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W2 ) )
    dep_pts_z  = field_type( vector_space = &
                          function_space_collection%get_fs( mesh_id, element_order, W2 ) )

    ! Create wind and density fields which live on the shifted coordinate field
    ! so that the finite-volume Cosmic transport scheme can be used to transport
    ! species living at Wtheta dofs.
    ! The shifted coordinate field has nlayers+1 with a half layer at the top and
    ! bottom of the column.
    wind_shifted    = field_type( vector_space = &
                          function_space_collection%get_fs( shifted_mesh_id, element_order, W2 ) )
    density_shifted = field_type( vector_space = &
                          function_space_collection%get_fs( shifted_mesh_id, element_order, W3 ) )

    ! Create runtime_constants object.
    call create_runtime_constants( mesh_id, twod_mesh_id, chi, shifted_mesh_id, shifted_chi )

    ! Initialise density field
    call transport_init_fields_alg( density )

    ! Set I/O behaviours for diagnostic output

    if ( write_diag .and. use_xios_io ) then
       ! Fields that are output on the XIOS face domain
       tmp_write_ptr => xios_write_field_face
       call wind_n%set_write_behaviour( tmp_write_ptr )
       call density%set_write_behaviour( tmp_write_ptr )
       call increment%set_write_behaviour( tmp_write_ptr )
       call divergence%set_write_behaviour( tmp_write_ptr )
    end if

    nullify( function_space, tmp_write_ptr )

  end subroutine init_transport

end module init_transport_mod
