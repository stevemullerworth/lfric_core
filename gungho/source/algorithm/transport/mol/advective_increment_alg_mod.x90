!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Compute the advective increment u.grad(f) for a wind field
!!        u and tracer f.

module advective_increment_alg_mod

  implicit none

  private

  public :: advective_increment_alg

contains

  !> @details Algorithm that uses the method-of-lines scheme to
  !!          compute the advective increment, u.grad(f), of field f
  !!          and wind u.
  !> @param[in,out] advective_increment Increment wind.grad(field)
  !> @param[in]     field               Field to compute the adevctive increment
  !> @param[in]     wind                Advecting wind
  !> @param[in]     dt                  Advection time step
  !> @param[in]     direction           Direction to perform the advective update
  !> @param[in]     transport_metadata  Contains transport configuration options
  !> @param[in]     final_rk_stage      Whether this is the last Runge-Kutta stage
  subroutine advective_increment_alg(advective_increment, field, wind,  &
                                     dt, direction, transport_metadata, &
                                     final_rk_stage)

    use constants_mod,                   only: r_def, i_def, l_def
    use field_mod,                       only: field_type
    use log_mod,                         only: log_event, &
                                               LOG_LEVEL_ERROR
    use reconstruct_w3_field_alg_mod,    only: reconstruct_w3_field_alg
    use w3_advective_update_kernel_mod,  only: w3_advective_update_kernel_type
    use w3h_advective_update_kernel_mod, only: w3h_advective_update_kernel_type
    use w3v_advective_update_kernel_mod, only: w3v_advective_update_kernel_type
    use wt_advective_update_alg_mod,     only: wt_advective_update_alg
    use fs_continuity_mod,               only: W3, Wtheta
    use operator_mod,                    only: operator_type
    use fem_constants_mod,               only: get_inverse_w3_mass_matrix
    use transport_metadata_mod,          only: transport_metadata_type
    use transport_enumerated_types_mod,  only: direction_3d, &
                                               direction_h,  &
                                               direction_v

    implicit none

    type(field_type),              intent(inout) :: advective_increment
    type(field_type),              intent(in)    :: field, wind
    real(kind=r_def),              intent(in)    :: dt
    integer(kind=i_def),           intent(in)    :: direction
    type(transport_metadata_type), intent(in)    :: transport_metadata
    logical(kind=l_def),           intent(in)    :: final_rk_stage

    type(field_type)             :: field_new
    type(operator_type), pointer :: m3_inv => null()

    select case ( field%which_function_space() )

      case ( W3 )
        call field_new%initialise( wind%get_function_space() )
        call reconstruct_w3_field_alg(field_new, field, wind, direction, &
                                      transport_metadata, final_rk_stage)

        m3_inv => get_inverse_w3_mass_matrix( field%get_mesh_id() )

        call invoke( setval_c(advective_increment, 0.0_r_def) )

        ! Different advective update kernel dependent on direction
        select case( direction )
        case ( direction_3d )
          call invoke( w3_advective_update_kernel_type(advective_increment, &
                                                       field_new, wind, m3_inv) )
        case ( direction_h )
          call invoke( w3h_advective_update_kernel_type(advective_increment, &
                                                        field_new, wind, m3_inv) )
        case ( direction_v )
          call invoke( w3v_advective_update_kernel_type(advective_increment, &
                                                        field_new, wind, m3_inv) )
        case default
          call log_event("Advective update direction not recognised", LOG_LEVEL_ERROR)
        end select

      case ( Wtheta )
        call wt_advective_update_alg(advective_increment, field, wind, dt, &
                                     direction, transport_metadata, final_rk_stage)
      case default
        call log_event( "Advective increment only valid for W3 & Wtheta spaces", LOG_LEVEL_ERROR )

    end select

  end subroutine advective_increment_alg

end module advective_increment_alg_mod
