!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module mm_diagonal_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: mm_diagonal_test_type
    private
    real(r_def),    allocatable :: mm(:,:,:)
    integer(i_def), allocatable :: map(:,:)
    real(r_def),    allocatable :: mm_diag(:), answer(:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type mm_diagonal_test_type

  integer(i_def), parameter :: undf  = 5, &
                               ndf   = 3, &
                               ncell = 2

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(mm_diagonal_test_type), intent(inout) :: this

    allocate( this%mm(ndf,ndf,ncell) )
    allocate( this%mm_diag(undf) )
    allocate( this%map(ndf,ncell) )
    allocate( this%answer(undf) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(mm_diagonal_test_type), intent(inout) :: this

    deallocate( this%mm, this%mm_diag, this%map, this%answer )

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use mm_diagonal_kernel_mod,     only : mm_diagonal_kernel_code

    implicit none

    class(mm_diagonal_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    integer(i_def), parameter :: nlayers = 1

    integer(i_def) :: df, cell

    ! Fill with some data
    do cell = 1,2
      this%mm(:,1,cell) = (/ 4.0_r_def, 2.0_r_def, 1.0_r_def /)
      this%mm(:,2,cell) = (/ 1.0_r_def, 6.0_r_def, 1.0_r_def /)
      this%mm(:,3,cell) = (/ 1.0_r_def, 2.0_r_def, 4.0_r_def /)
    end do
    this%map(:,1) = (/ 1, 2, 3 /)
    this%map(:,2) = (/ 3, 4, 5 /)
    this%mm_diag(:) = 0.0_r_def

    do cell = 1,ncell

      call mm_diagonal_kernel_code(cell,            &
                                   nlayers,         &
                                   this%mm_diag,    &
                                   ncell,           &
                                   this%mm,         &
                                   ndf,             &
                                   undf,            &
                                   this%map(:,cell) &
                                   )
    end do 
    this%answer = (/ 4.0_r_def, 6.0_r_def, 8.0_r_def, 6.0_r_def, 4.0_r_def /)
    @assertEqual(this%answer, this%mm_diag, tol)

  end subroutine test_all

end module mm_diagonal_kernel_mod_test
