!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! For further details please refer to the file LICENCE which you should have
! received as part of this distribution.
!-----------------------------------------------------------------------------

!> @brief Preconditioner for the gravity-wave system
!>
!> @details This type is derived from the abstract preconditioner type and
!> implements a preconditioner for the mixed gravity-wave system in the
!> variables \f$(\mathbf{U},P,B)\f$.
!>
!> The discretised mixed gravity-wave system for the velocity
!> (\f$\textbf{U}\f$), pressure (\f$P\f$) and buoyancy (\f$B\f$) unknowns is:
!>
!>   \f[ \mathbf{R}_u = M_2 \mathbf{U} - \alpha\Delta t (D^T P + Q B)   \f]
!>   \f[          R_p = P  + \alpha\Delta t c_s^2 M_3^{-1} D \mathbf{U} \f]
!>   \f[          R_b = M_b B + \alpha\Delta t N^2 Q^T \mathbf{U}       \f]
!>
!> Given the right hand side \f$(\mathbf{R}_u,R_p,R_b)\f$ this needs to be
!> solved for the state \f$(\mathbf{U},P,B)\f$.
!>
!> To construct a preconditioner, proceed as follows:
!>
!> * Approximately (via lumping the buoyancy mass matrix) eliminate buoyancy
!>   to obtain an equation for velocity and pressure only:
!>
!>   \f[ \textbf{R}^*_u = H_B \textbf{U} - \alpha\Delta t D^T P           \f]
!>   \f[            R_p = P + \alpha\Delta t c_s^2 M_3^{-1} D \textbf{U}  \f]
!>
!>   where
!>   \f[ \textbf{R}^*_u=\textbf{R}_u+\alpha\Delta t Q \tilde{M}_b^{-1} R_b  \f]
!>   \f[ H_B=\tilde{M}_2+\alpha^2(\Delta t)^2N^2\tilde{Q\tilde{M}_b^{-1}Q^T}\f]
!>
!>   and \f$\tilde{A}\f$ is the lumped version of a matrix \f$A\f$. Note that
!>   any lumped matrix is diagonal, so inversion is straightforward. The lumped
!>   matrices are represented as fields.
!>
!> * Eliminate velocity to obtain a Helmholtz-equation for pressure only:
!>
!>   \f[ R^*_p = H P \f]
!>
!>   where
!>
!>   \f[ R^*_p=R_p-\alpha\Delta t c_s^2 M_3^{-1} D H_B^{-1}\textbf{R}_u^* \f]
!>
!>   and the Helmholtz operator is given as
!>
!>   \f[ H = Id + \alpha^2(\Delta t)^2 c_s^2 M_3^{-1} D H_B^{-1} D^T\f]
!>
!> * Given the solution \f$P\f$ of the Helmholtz-system, the velocity and
!>   buoyancy are reconstructed as
!>
!>   \f[ \textbf{U} = H_B^{-1} (\mathbf{R}^*_u + \alpha\Delta t D^T P)     \f]
!>   \f[ B = \tilde{M}_b^{-1}(R_b - \alpha\Delta t N^2 D^T Q^T \textbf{U}) \f]
!>
!> More specifically, the preconditioner proceeds along the following steps
!>
!> 1. Calculate \f$\textbf{R})u^*\f$ and then \f$R^*_p\f$
!> 2. Approximately solve the Helmholtz-system \f$R^*_p = H P\f$ with an iterative
!>    solver
!> 3. Given \f$P\f$, reconstruct the velocity \f$\textbf{U}\f$ and
!>    buoyancy \f$B\f$
!>
!> The Helmholtz system is solved with an iterative solver which is passed
!> in the constructor.

module gw_mixed_schur_precon_alg_mod

  use vector_mod,                      only: abstract_vector_type
  use field_mod,                       only: field_type
  use field_vector_mod,                only: field_vector_type
  use constants_mod,                   only: i_def, r_def
  use fem_constants_mod,               only: get_inverse_w3_mass_matrix, &
                                             get_mass_matrix, &
                                             get_div
  use limited_area_constants_mod,      only: get_mask
  use initial_temperature_config_mod,  only: bvf_square
  use timestepping_config_mod,         only: dt
  use gravity_wave_constants_config_mod, only: cs_square,  &
                                               b_space,    &
                                               b_space_w0, &
                                               b_space_w3, &
                                               b_space_wtheta
  use operator_mod,                    only: operator_type
  use preconditioner_mod,              only: abstract_preconditioner_type
  use iterative_solver_mod,            only: abstract_iterative_solver_type
  use field_indices_mod,               only: igw_u, igw_p, igw_b
  use io_config_mod,                   only: subroutine_timers
  use timer_mod,                       only: timer
  use log_mod,                         only: log_event,       &
                                             LOG_LEVEL_ERROR, &
                                             LOG_LEVEL_INFO,  &
                                             LOG_LEVEL_DEBUG, &
                                             log_scratch_space
  use mesh_mod,                        only: mesh_type
  use fs_continuity_mod,           only: W0, W2, W3, Wtheta
  use boundaries_config_mod,       only: limited_area

  implicit none

  private

  !> @brief Mixed preconditioner type
  !>
  !> @details Implements mixed preconditioner which can be used in the
  !> iterative solver algorithms.
  type, public, extends(abstract_preconditioner_type) :: &
                        gw_mixed_schur_preconditioner_type

     private

     !> Off-centering parameter \f$\alpha\f$ in timestepping
     real(kind=r_def) :: alpha
     !> \f$Q\f$- matrix (projection in buoyancy equation)
     type(operator_type) :: Q
     !> increments for pressure, velocity and buoyancy
     type(field_type) :: p_inc, b_inc, u_inc
     !> temporary variable in pressure construction
     type(field_type) :: rhs_p_tmp
     !> modified right-hand \f$\textbf{R}_u^*\f$
     type(field_type) :: rhs_u
     !> modified right-hand \f$R_p^*\f$
     type(field_type) :: rhs_p
     !> 1-component field vector for right hand side in pressure system
     type(field_vector_type) :: pressure_b
     !> 1-component field vector for solution of pressure system
     type(field_vector_type) :: pressure_x
     !> temporary variable \f$H_B^{-1}\textbf{R}_u\f$
     type(field_type) :: hb_ru
     !> temporary variable \f$\tilde{M}_B^{-1}R_b\f$
     type(field_type) :: mb_rb
     !> lumped inverse \f$\tilde{M}^{-1}_{\theta}\f$
     type(field_type) :: Mb_lumped_inv, Hb_lumped_inv
     !> Pressure (Helmholtz) solver object
     class(abstract_iterative_solver_type), pointer :: &
        gw_pressure_solver

     contains

     ! Overide the (abstract interface) for application of
     ! a preconditioner \f$y = P.x\f$
     procedure, public :: apply => apply_gw_mixed_schur_preconditioner
     procedure, private :: apply_gw_mixed_schur_preconditioner

     !> private methods
     !> Build the right-hand-side for the Helmholtz equation
     procedure, private :: build_pressure_rhs
     !> Reconstruct the velocity and buoyancy from the solution of the
     !> Helmholtz equation
     procedure, private :: back_substitute

     !> Performs a deep copy (for overriding the "=" operator)
     procedure, public  :: gw_mixed_schur_preconditioner_assign

     !> Destructor
     final :: destroy_gw_mixed_schur_preconditioner

     !> Override default assignment for gw_mixed_schur_preconditioner_type pairs.
     generic            :: assignment(=) => gw_mixed_schur_preconditioner_assign

  end type gw_mixed_schur_preconditioner_type

  ! Overload the default structure constructor
  interface gw_mixed_schur_preconditioner_type
     module procedure gw_mixed_schur_preconditioner_constructor
  end interface

contains

  !> @brief Construct a <code> gw_mixed_schur_preconditioner_type</code> object
  !>
  !> @details This constructs all required fields and builds operators such as
  !> the lumped buoyancy mass matrix \f$\tilde{M}_b\f$ and the matrix
  !> \f$H_B\f$. The current state is passed in as a field vector to be able
  !> to extract the relevant function spaces.
  !>
  !> The matrix \f$\tilde{H}_b^{-1}\f$ is passed in as an argument since this
  !> matrix is already required for and constructed in the pressure operator.
  !>
  !> @param[in] state           Current state (passed as a field-vector)
  !> @param[in] Hb_lumped_inv   Lumped inverse \f$\tilde{H}_b^{-1}\f$
  !> @param[in] pressure_solver Solver object for Helmholtz system
  !> @return self The constructed preconditioner object
  function gw_mixed_schur_preconditioner_constructor(state,           &
                                                     Hb_lumped_inv,   &
                                                     pressure_solver) &
                                                     result(self)

    use function_space_mod,            only: function_space_type
    use finite_element_config_mod,     only: element_order
    use quadrature_xyoz_mod,           only: quadrature_xyoz_type
    use quadrature_rule_gaussian_mod,  only: quadrature_rule_gaussian_type
    use compute_q_operator_kernel_mod, only: compute_q_operator_type
    use matrix_vector_kernel_mod,      only: matrix_vector_kernel_type
    use transpose_matrix_vector_kernel_mod, &
         only: transpose_matrix_vector_kernel_type

    implicit none

    type(field_vector_type), intent(in) :: state
    type(field_type), intent(in) :: Hb_lumped_inv
    class(abstract_iterative_solver_type), target, intent(in) :: &
      pressure_solver
    type(gw_mixed_schur_preconditioner_type)  :: self
    type(function_space_type), pointer        :: wp => null(), &
                                                 wu => null(), &
                                                 wb => null()
    type(field_type)                          :: ones, M_lumped
    type(operator_type), pointer              :: Mb => null()
    type(quadrature_xyoz_type)                :: qr
    type(quadrature_rule_gaussian_type)       :: quadrature_rule
    type(mesh_type), pointer                  :: mesh => null()

    call log_event( 'Constructing gravity waves mixed preconditioner...', &
                    LOG_LEVEL_INFO )

    self%alpha = 0.5_r_def

    mesh => state%vector(igw_u)%get_mesh()

    ! Get required function spaces
    wu => state%vector(igw_u)%get_function_space()
    wp => state%vector(igw_p)%get_function_space()
    wb => state%vector(igw_b)%get_function_space()

    ! Create fields for increments
    call self%p_inc%initialise( vector_space = wp )
    call self%b_inc%initialise( vector_space = wb )
    call self%u_inc%initialise( vector_space = wu )

    ! Create fields for right-hand-side
    call self%rhs_u%initialise( vector_space = wu )
    call self%rhs_p%initialise( vector_space = wp )
    call self%rhs_p_tmp%initialise( vector_space = wp )
    call self%mb_rb%initialise( vector_space = wb )
    call self%hb_ru%initialise( vector_space = wu )

    ! right hand side and solution as a one-component field-vector in
    ! pressure space
    self%pressure_b = field_vector_type(1)
    self%pressure_x = field_vector_type(1)

    ! Compute inverse approximate \f$ \tilde{M}_b^{-1} \f$
    call ones%initialise( vector_space = wb )
    call M_lumped%initialise( vector_space = wb )
    call self%Mb_lumped_inv%initialise( vector_space = wb )
    select case(b_space)
    case(b_space_w0)
       Mb => get_mass_matrix(W0, mesh%get_id())
    case(b_space_w3)
       Mb => get_mass_matrix(W3, mesh%get_id())
    case(b_space_wtheta)
       Mb => get_mass_matrix(Wtheta, mesh%get_id())
    end select
    call invoke( setval_c( ones,     1.0_r_def ),               &
                 setval_c( M_lumped, 0.0_r_def ),               &
                 matrix_vector_kernel_type(M_lumped, ones, Mb), &
                 X_divideby_Y(self%Mb_lumped_inv, ones, M_lumped) )

    ! Compute Q (projection) operator
    call self%Q%initialise( wu, wb )
    qr = quadrature_xyoz_type(element_order+2, quadrature_rule)
    call invoke( compute_q_operator_type(self%Q, qr) )

    ! Set \f$ \tilde{H}_b^{-1}\f$
    call hb_lumped_inv%copy_field(self%hb_lumped_inv)

    ! Set pressure solver
    self%gw_pressure_solver => pressure_solver

    nullify( mesh )

    call log_event( 'done', LOG_LEVEL_INFO )

  end function gw_mixed_schur_preconditioner_constructor

  !> @brief Performs a deep copy between gw_mixed_operator_type pairs
  !> (for overriding the "=" operator).
  !>
  !> @param[in]  source gw_mixed_schur_preconditioner_type rhs
  !> @param[out] dest   gw_mixed_schur_preconditioner_type lhs
  subroutine gw_mixed_schur_preconditioner_assign(dest, source)

    implicit none

    class(gw_mixed_schur_preconditioner_type), intent(in)  :: source
    class(gw_mixed_schur_preconditioner_type), intent(out) :: dest

    ! Deep copy of the contents of the pressure_operator_type
    dest%alpha = source%alpha
    dest%Q = source%Q
    call source%p_inc%copy_field(dest%p_inc)
    call source%b_inc%copy_field(dest%b_inc)
    call source%u_inc%copy_field(dest%u_inc)
    call source%rhs_p_tmp%copy_field(dest%rhs_p_tmp)
    call source%rhs_u%copy_field(dest%rhs_u)
    call source%rhs_p%copy_field(dest%rhs_p)
    dest%pressure_b = source%pressure_b
    dest%pressure_x = source%pressure_x
    call source%hb_ru%copy_field(dest%hb_ru)
    call source%mb_rb%copy_field(dest%mb_rb)
    call source%Mb_lumped_inv%copy_field(dest%Mb_lumped_inv)
    call source%Hb_lumped_inv%copy_field(dest%Hb_lumped_inv)
    dest%gw_pressure_solver => source%gw_pressure_solver

  end subroutine gw_mixed_schur_preconditioner_assign


  !> @brief Apply the preconditioner to calculate \f$y = P.x\f$ for the
  !> mixed system in velocity, pressure and buoyancy
  !>
  !> @details Apply the preconditioner in three steps, as described above:
  !> 1. Construct right-hand side for Helmholtz system
  !> 2. Approximately solve Helmholtz-system
  !> 3. Reconstruct velocity and buoyancy from pressure
  !>
  !> @param[in,out] self Instance of type gw_mixed_schur_preconditioner_type
  !> @param[in]     x    Field-vector containing the residuals
  !>                     \f$(\textbf{R}_u,R_p,r_b)\f$
  !> @param[in,out] y    Field-vector containing the solution
  !>                     \f$(\textbf{U},P,B)\f$ to be calculated
  subroutine apply_gw_mixed_schur_preconditioner(self, x, y)

    implicit none

    class(gw_mixed_schur_preconditioner_type), intent(inout) :: self
    class(abstract_vector_type),               intent(in)    :: x
    class(abstract_vector_type),               intent(inout) :: y

    if ( subroutine_timers ) call timer('gw_mixed_schur_precon_alg')
    select type(x)
    type is(field_vector_type)
       select type(y)
       type is(field_vector_type)
          call invoke( setval_c( y%vector(igw_u), 0.0_r_def ),  &
                       setval_c( y%vector(igw_p), 0.0_r_def ),  &
                       setval_c( y%vector(igw_b), 0.0_r_def ) )

          ! STEP 1: Build RHS for Helmholtz system
          call self%build_pressure_rhs(x)

          ! STEP 2: Solve Helmholtz system
          ! Krylov solver to obtain pressure increment
          call invoke (setval_c(self%p_inc, 0.0_r_def) )
          call self%pressure_x%import_field(self%p_inc,1)
          ! (import required to initialise field)
          ! copy RHS into 1-component field-vector
          call self%pressure_b%import_field(self%rhs_p,1)
          call log_event('Schur preconditioner pressure solve:', LOG_LEVEL_INFO)
          call self%gw_pressure_solver%apply(self%pressure_x,self%pressure_b)
          ! copy solution out of 1-component field-vector
          call self%pressure_x%export_field(self%p_inc,1)

          ! STEP 3: Back substitute to obtain other fields
          call self%back_substitute(y, x)
       class default
          write(log_scratch_space, '(A)') &
               "gw_mixed_schur_preconditioner_mod: incorrect vector_type argument y"
          call log_event(log_scratch_space, LOG_LEVEL_ERROR)
       end select

    class default
       write(log_scratch_space, '(A)') &
             "gw_mixed_schur_preconditioner_mod: incorrect vector_type argument x"
       call log_event(log_scratch_space, LOG_LEVEL_ERROR)
    end select

    if ( subroutine_timers ) call timer('gw_mixed_schur_precon_alg')

  end subroutine apply_gw_mixed_schur_preconditioner

  !> @brief Compute the right-hand-side for the Helmholtz-equation
  !>
  !> @details Given the right-hand side \f$(\textbf{R}_u,R_p,R_b)\f$ for the
  !> mixed system, build the right hand side \f$R^*_p\f$ for the Helmholtz
  !> equation.
  !>
  !> @param[in,out] self Instance of type gw_mixed_schur_preconditioner_type
  !> @param[in]     rhs0 Initial residuals \f$(\textbf{R}_u,R_p,R_b)\f$ of the
  !>                     mixed system.
  subroutine build_pressure_rhs(self,rhs0)

    use matrix_vector_kernel_mod,    only: matrix_vector_kernel_type
    use dg_matrix_vector_kernel_mod, only: dg_matrix_vector_kernel_type
    use enforce_bc_kernel_mod,       only: enforce_bc_kernel_type

    implicit none

    class(gw_mixed_schur_preconditioner_type), intent(inout) :: self
    type(field_vector_type),                   intent(in)    :: rhs0

    type(operator_type), pointer :: div => null(), M3_inv => null()
    type(field_type), pointer    :: w2_mask => null()
    type(field_type), pointer    :: w3_mask => null()

    real(kind=r_def)    :: const1, const2

    type(mesh_type), pointer :: mesh => null()

    mesh => rhs0%vector(igw_b)%get_mesh()

    ! rhs_u = R_u + alpha * dt * Q * Mb^-1 * R_b
    ! rhs_p = R_p - alpha * dt * cs^2 * M3^-1 * D * HB^-1 * rhs_u

    ! Compute rhs_u and enforce boundary conditions
    const1 = self%alpha*dt

    if (limited_area) then
      w2_mask => get_mask(W2, mesh%get_id())
      w3_mask => get_mask(W3, mesh%get_id())
    endif

    call invoke( X_times_Y(self%mb_rb,rhs0%vector(igw_b),self%Mb_lumped_inv), &
                 setval_c( self%rhs_u, 0.0_r_def ),                           &
                 matrix_vector_kernel_type(self%rhs_u, self%mb_rb, self%Q),   &
                 inc_aX_plus_Y(const1, self%rhs_u, rhs0%vector(igw_u)),       &
                 enforce_bc_kernel_type(self%rhs_u) )
    if (limited_area) then
      call invoke( inc_X_times_Y(self%rhs_u, w2_mask) )
    endif

    ! Compute rhs_p
    div    => get_div(mesh%get_id())
    M3_inv => get_inverse_w3_mass_matrix(mesh%get_id())
    const2 = -self%alpha*dt*cs_square
    call invoke( X_times_Y(self%hb_ru, self%rhs_u, self%Hb_lumped_inv),       &
                 dg_matrix_vector_kernel_type(self%rhs_p_tmp,self%hb_ru, div),&
                 dg_matrix_vector_kernel_type(self%rhs_p,self%rhs_p_tmp,M3_inv), &
                 inc_aX_plus_Y(const2, self%rhs_p, rhs0%vector(igw_p)) )
    if (limited_area) then
      call invoke( inc_X_times_Y(self%rhs_p, w3_mask) )
    endif

    call self%rhs_p%log_minmax(LOG_LEVEL_DEBUG,  'gw_pressure_rhs ')

    nullify( mesh )

  end subroutine build_pressure_rhs

  !> @brief Reconstruct velocity and buoyancy from pressure
  !>
  !> @details Given the pressure \f$P\f$ from the solution of the Helmholtz
  !> equation, reconstruct the velocity \f$\textbf{U}\f$ and buoyancy \f$B\f$.
  !>
  !> @param[in,out] self  Instance of type gw_mixed_schur_preconditioner_type
  !> @param[in,out] state Solution \f$(\textbf{U},P,B)\f$ of the mixed system
  !> @param[in]     rhs   Initial right hand side \f$(\textbf{R}_u,R_p,R_b)\f$
  subroutine back_substitute(self, state, rhs)

    use transpose_matrix_vector_kernel_mod, &
                                  only: transpose_matrix_vector_kernel_type
    use enforce_bc_kernel_mod,    only: enforce_bc_kernel_type

    implicit none

    class(gw_mixed_schur_preconditioner_type), intent(inout) :: self
    type(field_vector_type),                   intent(inout) :: state
    type(field_vector_type),                   intent(in)    :: rhs

    type(field_type),    pointer :: w3_mask => null()
    type(field_type),    pointer :: w2_mask => null()
    type(field_type),    pointer :: wtheta_mask => null()
    type(operator_type), pointer :: div => null()
    real(kind=r_def)             :: const1, const2
    integer(kind=i_def)          :: buoyancy_space

    type(mesh_type),     pointer :: mesh => null()

    mesh => self%u_inc%get_mesh()

    if (limited_area) then
      select case(b_space)
        case(b_space_w0)
          buoyancy_space = W0
        case(b_space_w3)
          buoyancy_space = W3
        case(b_space_wtheta)
          buoyancy_space = Wtheta
        case default
          call log_event( 'gravity_wave, gw_mixed_schur_precon_alg_mod: '//  &
                          'Invalid buoyancy space', LOG_LEVEL_ERROR )
      end select
      w3_mask => get_mask(W3, mesh%get_id())
      w2_mask => get_mask(W2, mesh%get_id())
      wtheta_mask => get_mask(buoyancy_space, mesh%get_id())

      ! Apply masks to pressure, from Helmholtz solve
      call invoke( inc_X_times_Y(self%p_inc, w3_mask) )
    endif

    ! Initialise increment fields
    call invoke( setval_c(self%u_inc, 0.0_r_def), &
                 setval_c(self%b_inc, 0.0_r_def) )
    ! U increment
    div => get_div(mesh%get_id())
    const1 = self%alpha*dt
    call invoke( transpose_matrix_vector_kernel_type(self%u_inc,       &
                                                     self%p_inc,       &
                                                     div),             &
                 inc_aX_plus_Y(const1, self%u_inc, self%rhs_u),        &
                 inc_X_times_Y(self%u_inc, self%Hb_lumped_inv),        &
                 enforce_bc_kernel_type(self%u_inc) )

    if (limited_area) then
      call invoke( inc_X_times_Y(self%u_inc, w2_mask) )
    endif

    ! b increment
    const2 = -self%alpha*dt*bvf_square
    call invoke( transpose_matrix_vector_kernel_type(self%b_inc,       &
                                                     self%u_inc,       &
                                                     self%Q),          &
                 inc_aX_plus_Y(const2, self%b_inc, rhs%vector(igw_b)), &
                 inc_X_times_Y(self%b_inc, self%Mb_lumped_inv) )

    if (limited_area) then
      call invoke( inc_X_times_Y(self%b_inc, wtheta_mask) )
    endif

    call self%u_inc%log_minmax(LOG_LEVEL_DEBUG,  'u increment')
    call self%p_inc%log_minmax(LOG_LEVEL_DEBUG,  'p increment')
    call self%b_inc%log_minmax(LOG_LEVEL_DEBUG,  'b increment')

    call invoke( inc_X_plus_Y(state%vector(igw_u), self%u_inc), &
                 inc_X_plus_Y(state%vector(igw_p), self%p_inc), &
                 inc_X_plus_Y(state%vector(igw_b), self%b_inc) )

    nullify(div)
    if (limited_area) then
      nullify(w3_mask)
      nullify(w2_mask)
      nullify(wtheta_mask)
    endif

    nullify(mesh)

  end subroutine back_substitute

  !> @brief Destructor
  !> @param[in,out] self Instance of type to be destroyed
  subroutine destroy_gw_mixed_schur_preconditioner(self)

    implicit none

    type(gw_mixed_schur_preconditioner_type), intent(inout) :: self

  end subroutine destroy_gw_mixed_schur_preconditioner

end module gw_mixed_schur_precon_alg_mod
