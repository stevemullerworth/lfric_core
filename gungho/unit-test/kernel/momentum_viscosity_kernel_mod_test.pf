!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module momentum_viscosity_kernel_mod_test

  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size, &
                                                  get_w2_m3x3_q3x3x3_size

  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap,      &
                                                  get_w2_m3x3_dofmap,      &
                                                  get_w3_m3x3_dofmap,      &
                                                  get_m3x3_stencil_dofmap_cross
  use get_unit_test_3x3x3_chi_mod,         only : get_w0_3x3x3_field
  use pFUnit_Mod
  use yaxt,                                only : xt_initialize, xt_finalize
  use mpi_mod,                             only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: momentum_viscosity_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type momentum_viscosity_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use chi_transform_mod,         only : init_chi_transforms
    use feign_config_mod,          only : feign_finite_element_config
    use mixing_config_mod,         only : method_3d_smag
    use finite_element_config_mod, only : cellshape_quadrilateral, &
                                          spherical_coord_system_xyz

    implicit none

    class(momentum_viscosity_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(      &
        cellshape=cellshape_quadrilateral, &
        coordinate_order=0_i_def,          &
        element_order=0_i_def,             &
        rehabilitate=.true.,               &
        spherical_coord_order=0_i_def,     &
        spherical_coord_system=spherical_coord_system_xyz, &
        vorticity_in_w1=.false. )

    call init_chi_transforms()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use chi_transform_mod,        only: final_chi_transforms
    use configuration_mod,        only: final_configuration

    implicit none

    class(momentum_viscosity_test_type), intent(inout) :: this

    ! Finalise namelists
    call final_chi_transforms()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use momentum_viscosity_kernel_mod, only : momentum_viscosity_code

    implicit none

    class(momentum_viscosity_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: viscosity_mu  = 10.0_r_def

    real(r_def) :: answer

    integer(i_def) :: i, j, k, df, icell

    ! Variables for testing momentum_viscosity
    integer(i_def) :: nlayers, ncells, cell
    integer(i_def) :: ndf_w0, undf_w0, ndf_w2, undf_w2, ndf_pid, undf_pid
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_w2(:,:)
    integer(i_def), allocatable :: map_pid(:,:)
    integer(i_def), allocatable :: stencil_map_w2(:,:,:)

    real(r_def), allocatable :: u(:)
    real(r_def), allocatable :: u_inc(:)
    real(r_def), allocatable :: chi1(:), chi2(:), chi3(:)
    real(r_def), allocatable :: panel_id(:)

    nlayers = 3
    ndf_pid = 1
    undf_pid = 9

    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w0_m3x3_dofmap(map_w0)

    call get_w2_m3x3_q3x3x3_size( ndf_w2, undf_w2, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w2_m3x3_dofmap(map_w2)
    call get_m3x3_stencil_dofmap_cross(stencil_map_w2, map_w2)

    call get_w3_m3x3_dofmap( map_pid, 1 )

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))
    allocate(panel_id(undf_pid))

    panel_id(:) = 1.0_r_def

    call get_w0_3x3x3_field(chi1, chi2, chi3, dx, dy, dz, &
                            map_w0, nlayers)

    ! Create the data
    allocate(u(undf_w2))
    u(:) = 12.0_r_def
    allocate(u_inc(undf_w2))
    u_inc(:) = 0.0_r_def

    cell = 5
    call momentum_viscosity_code( &
                                 nlayers, &
                                 u_inc, &
                                 u, &
                                 5, stencil_map_w2(:,:,cell), &
                                 chi1, &
                                 chi2, &
                                 chi3, &
                                 panel_id, &
                                 viscosity_mu, &
                                 ndf_w2, undf_w2, &
                                 map_w2(:,cell), &
                                 ndf_w0, undf_w0, map_w0(:,cell), &
                                 ndf_pid, undf_pid, map_pid(:,cell)  &
                                )

    answer = 0.0_r_def
    do df = 1, ndf_w2
      @assertEqual(answer, u_inc(map_w2(df,cell) ), tol)
    end do

    deallocate(u)
    deallocate(u_inc)
    deallocate(chi1)
    deallocate(chi2)
    deallocate(chi3)
    deallocate(panel_id)
    deallocate(map_w0)
    deallocate(map_w2)
    deallocate(map_pid)
    deallocate(stencil_map_w2)

  end subroutine test_all

end module momentum_viscosity_kernel_mod_test
