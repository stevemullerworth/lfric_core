!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the rrho computation
!>
module rrho_kernel_mod_test

  implicit none 


contains

  @test
  subroutine rrho_kernel_test()

    use pFUnit_Mod
    use function_space_build_mod,   only : fs_build, fs_destroy
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use function_space_mod,         only : function_space_type, W2, W3
    use rrho_kernel_mod, &
                                    only : rrho_code
    use constants_mod,              only : r_def
    use quadrature_mod,             only : quadrature_type, QR3

   implicit none
    type(function_space_type) :: fs
    type( quadrature_type), pointer :: qr => null()

    real(kind=r_def) :: tol
    real(kind=r_def), dimension(:), allocatable :: answer

    integer :: cell

! Required for calling kernel 
    type( field_type ) :: r_rho, u

    integer                 :: nlayers, nqp_v, nqp_h
    integer                 :: ndf_w2, ndf_w3
    integer                 :: undf_w2, undf_w3
    integer                 :: diff_dim_w2, dim_w3
    integer, pointer  :: map_w3(:), map_w2(:), orientation_w2(:) => null()

    type( field_proxy_type )        :: r_rho_proxy, u_proxy
    
    real(kind=r_def), allocatable  :: basis_w3(:,:,:,:), &
                                      diff_basis_w2(:,:,:,:) 

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
   
    tol = 1.0e-6_r_def  
    qr => qr%get_instance(QR3,9,3)

    ! build some function spaces ...
    call fs_build()
    
    ! Test kernel
    r_rho = field_type( vector_space = fs%get_instance( W3 ) )
    u     = field_type( vector_space = fs%get_instance( W2 ) )

    r_rho_proxy  = r_rho%get_proxy()
    u_proxy      = u%get_proxy()
    
    nlayers = r_rho_proxy%vspace%get_nlayers()
    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w3  = r_rho_proxy%vspace%get_ndf( )
    dim_w3  = r_rho_proxy%vspace%get_dim_space( )
    undf_w3 = r_rho_proxy%vspace%get_undf()
    allocate(basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    ndf_w2      = u_proxy%vspace%get_ndf( )
    diff_dim_w2 = u_proxy%vspace%get_dim_space_diff( )
    undf_w2     = u_proxy%vspace%get_undf()
    allocate(diff_basis_w2(diff_dim_w2,ndf_w2,nqp_h,nqp_v))

    call r_rho_proxy%vspace%compute_basis_function(basis_w3, ndf_w3,     & 
                                                   nqp_h, nqp_v, xp, zp)

    call u_proxy%vspace%compute_diff_basis_function(                     &
         diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

! Create the data
    u_proxy%data(:) = 2.0_r_def
    r_rho_proxy%data(:) = 0.0

    do cell = 1, r_rho_proxy%vspace%get_ncell()

       map_w3 => r_rho_proxy%vspace%get_cell_dofmap( cell )
       map_w2 => u_proxy%vspace%get_cell_dofmap( cell )
       orientation_w2 => u_proxy%vspace%get_cell_orientation( cell )

       call rrho_code( nlayers,                          &
                       r_rho_proxy%data,                 &
                       u_proxy%data,                     &
                       ndf_w3,                           &
                       undf_w3,                          &
                       map_w3,                           &
                       basis_w3,                         &
                       ndf_w2,                           &
                       undf_w2,                          &
                       map_w2,                           &
                       diff_basis_w2,                    &
                       orientation_w2,                   &
                       nqp_h,                            &
                       nqp_v,                            &
                       wh,                               &
                       wv                                &
                       )
    end do 
  
    deallocate(basis_w3, diff_basis_w2 )
    allocate( answer(undf_w3) )

! Simple test, check when u = const, div(u) = 0    
    answer(:) = 0.0_r_def
    @assertEqual(answer, r_rho_proxy%data(:), tol)

    deallocate( answer )
    !tear down ...
    call fs_destroy()
  
  end subroutine rrho_kernel_test

end module rrho_kernel_mod_test
