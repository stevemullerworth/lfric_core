!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!-----------------------------------------------------------------------------
!> @brief Computes vertical departure points used by the split-scheme.
!> @details The algorithm calculates the vertical departure points for the
!>          cell edges using only the vertical wind.
!-----------------------------------------------------------------------------
module calc_vertical_departure_alg_mod

  use constants_mod,                     only: r_def, i_def
  use biperiodic_deppt_config_mod,       only: n_dep_pt_iterations
  use io_config_mod,                     only: subroutine_timers
  use timer_mod,                         only: timer
  use field_mod,                         only: field_type
  use calc_departure_wind_kernel_mod,    only: calc_departure_wind_kernel_type
  use vertical_trapezoidal_kernel_mod,   only: vertical_trapezoidal_kernel_type
  use function_space_mod,                only: function_space_type
  use geometric_constants_mod,           only: get_coordinates, get_panel_id

  implicit none

  private
  public :: calc_vertical_departure

contains
!-----------------------------------------------------------------------------
!> @details Algorithm for calculating vertical departure points.
!> @param[in]     wind              Advecting wind field.
!> @param[in,out] dep_pts_z         Departure points in z direction.
!> @param[in]     dt                The model timestep length
!-----------------------------------------------------------------------------
  subroutine calc_vertical_departure( wind, dep_pts_z, dt )

    implicit none

    type( field_type ), intent(in)    :: wind
    type( field_type ), intent(inout) :: dep_pts_z
    real( kind=r_def ), intent(in)    :: dt

    type( field_type )                   :: dep_wind_n
    type( field_type )                   :: dep_wind_np1
    type( function_space_type ), pointer :: w2_fs => null()
    type( field_type ), pointer          :: chi_sph(:) => null()
    type( field_type ), pointer          :: panel_id => null()
    integer(kind=i_def)                  :: mesh_id

    mesh_id = wind%get_mesh_id()

    w2_fs => wind%get_function_space()
    call   dep_wind_n%initialise( vector_space = w2_fs )
    call dep_wind_np1%initialise( vector_space = w2_fs )
    chi_sph => get_coordinates(mesh_id)
    panel_id => get_panel_id(mesh_id)

    if ( subroutine_timers ) call timer( 'calc_vertical_departure' )

    ! Calculate vertical departure points and departure winds.
    ! Currently the trapezoidal method has been choosen for calculating departure
    ! points in the vertical.

    call invoke( name = "initialise vertical_departures",         &
                 setval_c(dep_pts_z, 0.0_r_def),                  &
                 setval_c(dep_wind_n, 0.0_r_def),                 &
                 setval_c(dep_wind_np1, 0.0_r_def)                )
    !
    ! These invokes are left ungrouped due to PSyclone issue #867.
    !
    call invoke( calc_departure_wind_kernel_type(dep_wind_n, wind, &
                                                 chi_sph, panel_id) )
    call invoke( setval_X(dep_wind_np1, dep_wind_n) )
    call invoke( vertical_trapezoidal_kernel_type(dep_pts_z, dep_wind_n, &
                                                  dep_wind_np1,          &
                                                  n_dep_pt_iterations, dt) )

    nullify( w2_fs, chi_sph, panel_id )

    if ( subroutine_timers ) call timer( 'calc_vertical_departure' )

  end subroutine calc_vertical_departure

end module calc_vertical_departure_alg_mod
