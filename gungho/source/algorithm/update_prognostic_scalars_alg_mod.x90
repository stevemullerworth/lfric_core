!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Update to the dynamics scalars based upon advection updates
module update_prognostic_scalars_alg_mod

  implicit none

  private

  public :: update_prognostic_scalars_alg
contains

  !> @details Compute the dynamics scalars (exner, rho, theta) based upon the
  !>          the latest advection update
  !> @param[in] state latest estimate for dynamics prognostics
  !> @param[in] rhs_n Timelevel n forcings
  !> @param[in] rhs_adv Advection forcings
  !> @param[in] moist_dyn_gas_law Gas law component of moist dynamics
  subroutine update_prognostic_scalars_alg(state, rhs_n, rhs_adv, moist_dyn_gas_law)

    use constants_mod,                only: i_def
    use derived_config_mod,           only: bundle_size
    use field_indices_mod,            only: igh_t, igh_d, igh_p
    use field_mod,                    only: field_type
    use quadrature_xyoz_mod,          only: quadrature_xyoz_type
    use operator_mod,                 only: operator_type
    use fem_constants_mod,            only: get_qr, &
                                            get_inverse_w3_mass_matrix
    use geometric_constants_mod,      only: get_coordinates, &
                                            get_panel_id
    use mass_matrix_solver_alg_mod,   only: mass_matrix_solver_alg
    use project_pressure_kernel_mod,  only: project_pressure_kernel_type

    implicit none

    type( field_type ), dimension(bundle_size), intent(inout) :: state
    type( field_type ), dimension(bundle_size), intent(in)    :: rhs_n, rhs_adv
    type( field_type ),                         intent(in)    :: moist_dyn_gas_law

    type( field_type )             :: dtheta
    type( field_type ),    pointer :: panel_id => null()
    type( field_type ),    pointer :: chi_sph(:) => null()
    type( operator_type ), pointer :: m3_inv => null()
    type( quadrature_xyoz_type ), pointer :: qr => null()

    integer(kind=i_def) :: mesh_id

    mesh_id = state(igh_t)%get_mesh_id()

    call dtheta%initialise( state(igh_t)%get_function_space() )

    m3_inv  => get_inverse_w3_mass_matrix(mesh_id)
    chi_sph => get_coordinates(mesh_id)
    panel_id => get_panel_id(mesh_id)
    qr => get_qr()

    ! rho^np1 and theta^np1 can be updated directly from advection terms
    ! rho^np1   = rho^n   - dt*div(u*rho)
    ! theta^np1 = theta^n - dt*u.grad(theta)
    call invoke( X_plus_Y(state(igh_d), rhs_adv(igh_d), rhs_n(igh_d)), &
                 X_plus_Y(dtheta,       rhs_adv(igh_t), rhs_n(igh_t)) )
    call mass_matrix_solver_alg(state(igh_t), dtheta)

    call invoke( project_pressure_kernel_type(state(igh_p), state(igh_d),      &
                                              state(igh_t), moist_dyn_gas_law, &
                                              chi_sph, panel_id, m3_inv, qr ) )

    nullify( chi_sph, m3_inv, panel_id, qr )
  end subroutine update_prognostic_scalars_alg

end module update_prognostic_scalars_alg_mod
