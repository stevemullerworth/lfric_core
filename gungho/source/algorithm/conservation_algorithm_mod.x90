!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Algorithm to compute various conserved quantities 
!>@details Computes various conserved quantities and checks how
!> accurately they have been conserved
module conservation_algorithm_mod

  use compute_total_aam_kernel_mod,    only: compute_total_aam_kernel_type
  use compute_total_energy_kernel_mod, only: compute_total_energy_kernel_type
  use compute_total_mass_kernel_mod,   only: compute_total_mass_kernel_type
  use compute_total_pv_kernel_mod,     only: compute_total_pv_kernel_type

  use constants_mod,                   only: r_def, i_def
  use function_space_collection_mod,   only: function_space_collection
  use field_mod,                       only: field_type
  use finite_element_config_mod,       only: element_order, wtheta_on
  use fs_continuity_mod,               only: W3
  use function_space_mod,              only: function_space_type
  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO
  use psykal_lite_mod,                 only: invoke_sum_field
  use quadrature_mod,                  only: quadrature_type, GAUSSIAN
  use runtime_constants_mod,           only: get_geopotential, &
                                             get_coordinates
  use output_config_mod,               only: subroutine_timers 
  use timer_mod,                       only: timer

implicit none

private
public :: conservation_algorithm

contains

  !>@brief Algorithm to compute various conserved quantities
  !>@details Computes various conserved quantities and checks how
  !> accurately they have been conserved
  !> @param[in] tstep Current timestep
  !> @param[in] rho Density field
  !> @param[in] u Velocity field
  !> @param[in] theta Potential temperature field
  !> @param[in] xi Vorticity field
  subroutine conservation_algorithm( tstep,        &
                                     rho,          &
                                     u,            &
                                     theta,        &
                                     xi)
  implicit none
    integer, intent(in) :: tstep


    ! prognostic fields    
    type( field_type ), intent( inout ) :: u, rho, theta, xi

    ! local variables
    type( field_type ), pointer :: geopotential => null()
    type(quadrature_type)       :: qr
    ! coordinate fields
    type( field_type ), pointer :: chi(:) => null()

    type(function_space_type), pointer :: w3_fs => null()

    type( field_type ) :: mass, pv, aam, energy
    real(kind=r_def) :: total_mass, total_pv, total_aam, total_energy 
    integer(i_def)   :: mesh 

    if ( subroutine_timers ) call timer('conservation_alg')

    geopotential => get_geopotential()
    chi          => get_coordinates()

    ! Get a quadrature rule
    qr = quadrature_type(element_order+3,GAUSSIAN)
    mesh = rho%get_mesh_id()
    w3_fs => function_space_collection%get_fs( mesh, element_order, W3)
   
    ! Compute Mass
    mass = field_type( vector_space = w3_fs )
    call invoke( compute_total_mass_kernel_type(mass, rho, chi, qr) )
    call invoke_sum_field(mass, total_mass)

    ! Compute Total Energy
    energy = field_type( vector_space = w3_fs )
    call invoke( compute_total_energy_kernel_type(energy, u, rho, theta, &
                                                  geopotential, chi, qr) )
    call invoke_sum_field(energy, total_energy)

    ! Gradient of theta not defined for the Wtheta case, TBD
    if (.not. wtheta_on) then
      ! Compute Ertel PV
      pv = field_type( vector_space = w3_fs )
      call invoke( compute_total_pv_kernel_type(pv, xi, theta, chi, qr) )
      call invoke_sum_field(pv, total_pv)
    end if

    ! Compute axial angular momentum
    aam = field_type( vector_space = w3_fs )
    call invoke( compute_total_aam_kernel_type(aam, u, rho, chi, qr) )
    call invoke_sum_field(aam, total_aam)

    if (wtheta_on) then
      write( log_scratch_space, '(A,I4,4E32.24)')  &
             'Conservation  = ',&
              tstep, total_mass, total_energy, total_aam
    else
      write( log_scratch_space, '(A,I4,4E32.24)')  &
             'Conservation  = ',&
              tstep, total_mass, total_energy, total_pv, total_aam
    end if
    call log_event( log_scratch_space, LOG_LEVEL_INFO )
    if ( subroutine_timers ) call timer('conservation_alg')

  end subroutine conservation_algorithm

end module conservation_algorithm_mod
