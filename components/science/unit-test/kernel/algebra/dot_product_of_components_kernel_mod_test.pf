!-----------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the computation of a dot product of a vector defined at scalar points with itself
!>
module dot_product_of_components_kernel_mod_test

  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w3_m3x3_q3x3x3_size
  use get_unit_test_m3x3_dofmap_mod,       only : get_w3_m3x3_dofmap
  use funit

  implicit none

  private
  public :: dot_product_of_components_test_type, test_all

  @TestCase
  type, extends(TestCase) :: dot_product_of_components_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type dot_product_of_components_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )



    implicit none

    class(dot_product_of_components_test_type), intent(inout) :: this

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use config_loader_mod, only: final_configuration

    implicit none

    class(dot_product_of_components_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env, only : real64
    use sci_dot_product_of_components_kernel_mod, only : dot_product_of_components_code

    implicit none

    class(dot_product_of_components_test_type), intent(inout) :: this

    real(r_def), parameter :: tol    = 1.0e-9_r_def   ! r_def 64bit
    real(r_def), parameter :: tol32  = 1.0e-6_r_def   ! r_def 32bit
    real(r_def), parameter :: answer = 14.0_r_def
    real(r_def), parameter :: dx     = 4.0_r_def,  &
                              dy     = 20.0_r_def, &
                              dz     = 8.0_r_def

    real(r_def)            :: use_tol

    integer(i_def) :: nlayers, ncells, ncell_3d, cell, icell
    integer(i_def) :: ndf_w3, undf_w3
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def) :: i, j, k

    integer(i_def), allocatable :: map_w3(:,:)

    real(r_def), allocatable :: dot_prod(:)
    real(r_def), allocatable :: field_x(:)
    real(r_def), allocatable :: field_y(:)
    real(r_def), allocatable :: field_z(:)

    nlayers=3
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w3_m3x3_dofmap(map_w3)

    ! Create the data
    cell = 1
    allocate(dot_prod(undf_w3))
    allocate(field_x(undf_w3))
    allocate(field_y(undf_w3))
    allocate(field_z(undf_w3))

    ! Set most field values to be zero
    field_x(:) = 0.0_r_def
    field_y(:) = 0.0_r_def
    field_z(:) = 0.0_r_def
    dot_prod(:) = 0.0_r_def

    ! Vector in cell 1 should be [2,1,3]
    field_x(map_w3(1, cell)) = 2.0_r_def
    field_y(map_w3(1, cell)) = 1.0_r_def
    field_z(map_w3(1, cell)) = 3.0_r_def

    call dot_product_of_components_code(  nlayers,                  &
                                          dot_prod,                 &
                                          field_x,                  &
                                          field_y,                  &
                                          field_z,                  &
                                          ndf_w3, undf_w3,          &
                                          map_w3(:,cell) )

    if ( r_def == real64 ) then
      use_tol = tol
    else
      use_tol = tol32
    end if

    ! dot_prod of vector [2,1,3] should be 2*2 + 1*1 + 3*3 = 14
    @assertEqual(answer, dot_prod(map_w3(1, cell)), use_tol)

    deallocate(map_w3)
    deallocate(dot_prod)
    deallocate(field_x)
    deallocate(field_y)
    deallocate(field_z)

  end subroutine test_all

end module dot_product_of_components_kernel_mod_test
