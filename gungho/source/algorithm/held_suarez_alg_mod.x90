!-------------------------------------------------------------------------------
! (C) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!>@brief Interface to Held-Suarez code
module held_suarez_alg_mod

  use physics_config_mod,             only: heldsuarez_thermo_placement,    &
                                            heldsuarez_wind_placement
  use log_mod,                        only: log_event, LOG_LEVEL_INFO

  use held_suarez_fv_kernel_mod,      only: held_suarez_fv_kernel_type
  use held_suarez_fv_wind_kernel_mod, only: held_suarez_fv_wind_kernel_type

  ! Derived Types
  use constants_mod,                  only: i_def, r_def
  use field_mod,                      only: field_type
  use field_collection_mod,           only: field_collection_type

  use fs_continuity_mod,              only: W2
  use fem_constants_mod,              only: get_rmultiplicity
  use geometric_constants_mod,        only: get_coordinates, &
                                            get_da_at_w2,    &
                                            get_panel_id

  use io_config_mod,                  only: subroutine_timers
  use timer_mod,                      only: timer

  implicit none

  private
  public :: held_suarez_alg

contains

  !>@brief Interface to Held-Suarez theta and wind kernels
  !> @param[in]     derived_fields         Group of derived fields
  !> @param[in]     theta                  Theta field
  !> @param[in]     exner                  Exner pressure
  !> @param[in]     exner_in_wth           Exner pressure on wtheta space
  !> @param[out]    dtheta_hs              Theta increment from HS code
  !> @param[out]    du_hs                  Wind increment from HS code
  !> @param[out]    dt                     The model timestep length
  !> @param[in,out] held_suarez_thermo_done Flag to say if thermo code run
  !> @param[in,out] held_suarez_wind_done   Flag to say if wind code run
  subroutine held_suarez_alg(derived_fields, theta, exner, exner_in_wth,   &
                             dtheta_hs, du_hs, dt,                         &
                             held_suarez_thermo_done, held_suarez_wind_done)

    implicit none

    type( field_collection_type ), intent(in) :: derived_fields
    type( field_type ), intent( in )  :: theta, exner, exner_in_wth
    type( field_type ), intent( out ) :: dtheta_hs, du_hs
    real( kind=r_def ), intent( in )  :: dt

    logical, intent(inout) :: held_suarez_thermo_done, held_suarez_wind_done

    ! Temporary field to unpack from field collection
    type( field_type ), pointer :: u_physics => null()
    type( field_type ), pointer :: w2_rmultiplicity => null()
    type( field_type ), pointer :: dA => null()
    type( field_type ), pointer :: chi_sph(:) => null()
    type( field_type ), pointer :: panel_id => null()
    integer(kind=i_def) :: mesh_id
    if ( subroutine_timers ) call timer("held_suarez_alg")

    mesh_id = theta%get_mesh_id()

    if (heldsuarez_thermo_placement < 0)then

      call dtheta_hs%initialise( vector_space = theta%get_function_space() )
      call invoke( setval_c(dtheta_hs, 0.0_r_def) )

      call log_event( &
         'slow_physics: Running Held-Suarez theta forcing', &
         LOG_LEVEL_INFO )
      chi_sph => get_coordinates(mesh_id)
      panel_id => get_panel_id(mesh_id)
      call invoke(held_suarez_fv_kernel_type(dtheta_hs, theta, exner_in_wth, &
                                             chi_sph, panel_id, dt))
      held_suarez_thermo_done=.true. ! Collect this increment later on

    end if

    if (heldsuarez_wind_placement < 0)then

      u_physics => derived_fields%get_field('u_physics')
      w2_rmultiplicity => get_rmultiplicity( W2, mesh_id ) ! 1/multiplicity of w2

      call du_hs%initialise( vector_space = u_physics%get_function_space() )
      call invoke( setval_c(du_hs, 0.0_r_def) )

      call log_event( &
         'slow_physics: Running Held-Suarez wind forcing', &
         LOG_LEVEL_INFO )
      dA => get_da_at_w2(mesh_id)
      call invoke(held_suarez_fv_wind_kernel_type(du_hs, u_physics,   &
                                                  w2_rmultiplicity,   &
                                                  exner,              &
                                                  exner_in_wth, dt),  &
                  inc_X_times_Y(du_hs, dA) )
      held_suarez_wind_done=.true. ! Collect this increment later on

      nullify( w2_rmultiplicity )

    end if

    if ( subroutine_timers ) call timer("held_suarez_alg")

  end subroutine held_suarez_alg

end module held_suarez_alg_mod
