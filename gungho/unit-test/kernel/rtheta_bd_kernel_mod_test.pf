!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the rtheta computation
!>
module rtheta_bd_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : Wtheta, W2
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN
  use stencil_dofmap_mod,            only : stencil_dofmap_type, STENCIL_CROSS


  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: rtheta_bd_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: wtheta_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: r_theta_bd, u
    real(r_def), allocatable           :: basis_w2_face(:,:,:,:,:), &
                                          basis_wtheta_face(:,:,:,:,:)

    type(stencil_dofmap_type), pointer :: cross_stencil_w2 => null()
    type(stencil_dofmap_type), pointer :: cross_stencil_wtheta => null()

    integer, pointer        :: cross_stencil_w2_map(:,:,:) => null()
    integer                 :: cross_stencil_w2_size

    integer, pointer        :: cross_stencil_wtheta_map(:,:,:) => null()
    integer                 :: cross_stencil_wtheta_size

  contains
    procedure setup
    procedure tearDown
    procedure test_all
  end type rtheta_bd_test_type

  integer(i_def) :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only: reference_cube, reference_element, nfaces_h

    implicit none

    class(rtheta_bd_test_type), intent(inout) :: this


    type(field_proxy_type) :: r_theta_bd_proxy, u_proxy

    integer(i_def)         :: nqp_h, nqp_v, nqp_h_1d
    integer(i_def)         :: ndf_wtheta, undf_wtheta, dim_wtheta
    integer(i_def)         :: ndf_w2, undf_w2, dim_w2
    integer(i_def)         :: mesh_id

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Assumes same number of horizontal quad points in x and y
    nqp_h_1d = int(nqp_h**(1./2.))

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%wtheta_fs => function_space_collection%get_fs(mesh_id, element_order, Wtheta)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)

    this%u  = field_type( vector_space = this%w2_fs )
    u_proxy = this%u%get_proxy()
    ndf_w2  = u_proxy%vspace%get_ndf( )
    dim_w2  = u_proxy%vspace%get_dim_space( )
    allocate( this%basis_w2_face(nfaces_h,dim_w2,ndf_w2,nqp_h_1d,nqp_v) )

    this%r_theta_bd  = field_type( vector_space = this%wtheta_fs )
    r_theta_bd_proxy = this%r_theta_bd%get_proxy()
    ndf_wtheta        = r_theta_bd_proxy%vspace%get_ndf( )
    dim_wtheta        = r_theta_bd_proxy%vspace%get_dim_space( )
    undf_wtheta       = r_theta_bd_proxy%vspace%get_undf()
    allocate( this%basis_wtheta_face(nfaces_h,dim_wtheta,ndf_wtheta,nqp_h_1d,nqp_v) )

    this%cross_stencil_w2 => u_proxy%vspace%get_stencil_dofmap(STENCIL_CROSS, 1)
    this%cross_stencil_w2_map => this%cross_stencil_w2%get_whole_dofmap()
    this%cross_stencil_w2_size = this%cross_stencil_w2%get_size()

    this%cross_stencil_wtheta => r_theta_bd_proxy%vspace%get_stencil_dofmap(STENCIL_CROSS, 1)
    this%cross_stencil_wtheta_map => this%cross_stencil_wtheta%get_whole_dofmap()
    this%cross_stencil_wtheta_size = this%cross_stencil_wtheta%get_size()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(rtheta_bd_test_type), intent(inout) :: this

    deallocate( this%basis_w2_face,      &
                this%basis_wtheta_face   &
                )

    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use rtheta_bd_kernel_mod,          only : rtheta_bd_code
    use reference_element_mod,         only : nfaces_h
    use mesh_mod,                      only : mesh_type

    implicit none

    class(rtheta_bd_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: answer = 1.0_r_def


    integer :: cell

    ! Required for calling kernel 
    type(field_type)  :: theta

    integer                 :: nlayers, nqp_h, nqp_v, nqp_h_1d
    integer                 :: ndf_wtheta, undf_wtheta
    integer                 :: ndf_w2, undf_w2

    integer                 :: ff, ii, jj

    type(field_proxy_type)  :: r_theta_bd_proxy, theta_proxy, &
                               u_proxy

    real(kind=r_def), pointer :: xp(:,:), xp_f(:,:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:)   => null(), &
                                 wv(:)   => null()

    integer(i_def), pointer :: adjacent_face(:,:) => null()
    type(mesh_type), pointer :: mesh => null()
    ! Test kernel
    theta   = field_type( vector_space = this%wtheta_fs )

    r_theta_bd_proxy   = this%r_theta_bd%get_proxy()
    theta_proxy        = theta%get_proxy()
    u_proxy            = this%u%get_proxy()

    nlayers = r_theta_bd_proxy%vspace%get_nlayers()

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ! Assumes same number of horizontal quad points in x and y
    nqp_h_1d = int(nqp_h**(1./2.))

    allocate(xp_f(nfaces_h, nqp_h_1d, 2))

    ndf_w2      = u_proxy%vspace%get_ndf( )
    undf_w2     = u_proxy%vspace%get_undf()

    ndf_wtheta      = r_theta_bd_proxy%vspace%get_ndf( )
    undf_wtheta     = r_theta_bd_proxy%vspace%get_undf()

    ! Quadrature points on horizontal faces

    xp_f(1, :, :) = xp(1:nqp_h_1d, :)
    xp_f(1, :, 1) = 0.0_r_def

    xp_f(2, :, :) = xp(1:nqp_h - nqp_h_1d + 1:nqp_h_1d, :)
    xp_f(2, :, 2) = 0.0_r_def

    xp_f(3, :, :) = xp(nqp_h - nqp_h_1d + 1:nqp_h, :)
    xp_f(3, :, 1) = 1.0_r_def

    xp_f(4, :, :) = xp(nqp_h_1d:nqp_h:nqp_h_1d, :)
    xp_f(4, :, 2) = 1.0_r_def

    do ff = 1, nfaces_h

      call u_proxy%vspace%compute_basis_function( &
        this%basis_w2_face(ff,:,:,:,:), ndf_w2, nqp_h_1d, nqp_v, xp_f(ff, :, :), zp)

      call theta_proxy%vspace%compute_basis_function( &
        this%basis_wtheta_face(ff,:,:,:,:), ndf_wtheta, nqp_h_1d, nqp_v, xp_f(ff, :, :), zp)

    end do

    ! Create the data
    r_theta_bd_proxy%data(:) = 1.0_r_def
    theta_proxy%data(:) = 3000.0_r_def
    u_proxy%data(:) = 20.0_r_def

    mesh => this%r_theta_bd%get_mesh()
    adjacent_face => mesh%get_adjacent_face()
    cell = 5

    call rtheta_bd_code( nlayers,                                 &
                         ndf_w2, undf_w2,                         &
                         this%cross_stencil_w2_map(:,:,cell),     &
                         this%cross_stencil_w2_size,              &
                         ndf_wtheta, undf_wtheta,                 &
                         this%cross_stencil_wtheta_map(:,:,cell), &
                         this%cross_stencil_wtheta_size,          &
                         r_theta_bd_proxy%data,                   &
                         theta_proxy%data,                        &
                         u_proxy%data,                            &
                         nqp_v, nqp_h_1d, wv,                     &
                         this%basis_w2_face,                      &
                         this%basis_wtheta_face, adjacent_face(:,cell) )

    ! Simple test, check that when theta=constant, the code inputs no increment
    @assertEqual( answer, r_theta_bd_proxy%data(:), tol )

    deallocate(xp_f)

  end subroutine test_all

end module rtheta_bd_kernel_mod_test
