!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module sample_flux_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection

  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: sample_flux_test_type
    private
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: rho, u
    real(r_def), allocatable           :: basis_rho(:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type sample_flux_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(sample_flux_test_type), intent(inout) :: this

    type(field_proxy_type)    :: rho_p, u_p

    integer(i_def) :: ndf_rho, dim_rho, ndf_u
    integer(i_def) :: mesh_id

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%u = field_type( vector_space = this%w2_fs )
    u_p    = this%u%get_proxy()
    ndf_u  = u_p%vspace%get_ndf( )

    this%rho = field_type( vector_space = this%w3_fs )
    rho_p    = this%rho%get_proxy()
    ndf_rho  = rho_p%vspace%get_ndf( )
    dim_rho  = rho_p%vspace%get_dim_space( )

    allocate( this%basis_rho(dim_rho, ndf_rho, ndf_u) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(sample_flux_test_type), intent(inout) :: this

    deallocate( this%basis_rho )

    call mesh_collection%clear()
    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use sample_flux_kernel_mod,     only : sample_flux_code
    use evaluate_function_mod,      only : BASIS

    implicit none

    class(sample_flux_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    real(r_def) :: answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Variables for testing sample_flux
    type(field_type) :: flux, m

    integer          :: ndf_rho, undf_rho, dim_rho, &
                        ndf_u, undf_u, dim_u,       &
                        ndf_flux, df_flux, df_rho

    real(kind=r_def), pointer :: nodes_flux(:,:) => null()
    integer, pointer          :: map_rho(:)      => null()
    integer, pointer          :: map_u(:)        => null() 

    type(field_proxy_type)    :: flux_p, rho_p, u_p, m_p

    ! Test flux computation
    flux = field_type( vector_space = this%w2_fs )
    m    = field_type( vector_space = this%w2_fs )

    flux_p    = flux%get_proxy()
    rho_p     = this%rho%get_proxy()
    u_p       = this%u%get_proxy()
    m_p       = m%get_proxy()

    ndf_u  = u_p%vspace%get_ndf( )
    undf_u = u_p%vspace%get_undf( )

    ndf_rho  = rho_p%vspace%get_ndf( )
    undf_rho = rho_p%vspace%get_undf( )
    dim_rho  = rho_p%vspace%get_dim_space( )

    nodes_flux => flux_p%vspace%get_nodes()
    ndf_flux   = flux_p%vspace%get_ndf()

    ! Evaluate the basis function
    do df_flux = 1, ndf_flux
      do df_rho = 1, ndf_rho
        this%basis_rho(:,df_rho,df_flux) = rho_p%vspace%evaluate_function(BASIS,df_rho,nodes_flux(:,df_flux))
      end do
    end do

    cell = 5
    map_u   => u_p%vspace%get_cell_dofmap( cell )
    map_rho => rho_p%vspace%get_cell_dofmap( cell )
    ! Create the data
    flux_p%data(:) = 0.0_r_def
    u_p%data(:) = 0.0_r_def
    rho_p%data(:) = 2.0_r_def
    m_p%data(:) = 2.0_r_def

    u_p%data(map_u(5)+0) = 0.0_r_def
    u_p%data(map_u(5)+1) = 1.0_r_def
    u_p%data(map_u(6)+1) = 1.0_r_def
    u_p%data(map_u(6)+2) = 0.0_r_def
    rho_p%data(map_rho(1)) = 3.0_r_def
    rho_p%data(map_rho(1)+1) = 4.0_r_def

    call sample_flux_code(flux_p%vspace%get_nlayers(), &
                          flux_p%data, & 
                          m_p%data, &
                          u_p%data, &
                          rho_p%data, &
                          ndf_u, & 
                          undf_u, &
                          map_u, &
                          ndf_rho, &
                          undf_rho, &
                          map_rho, &
                          this%basis_rho &
                          )

     answer = 0.0_r_def 
     @assertEqual(answer, flux_p%data(map_u(1) ), tol)
     answer = 0.0_r_def 
     @assertEqual(answer, flux_p%data(map_u(5) ), tol)
     answer = 3.5_r_def 
     @assertEqual(answer, flux_p%data(map_u(6) ), tol)
     answer = 3.0_r_def 
     @assertEqual(answer, flux_p%data(map_u(5)+ 2 ), tol)

  end subroutine test_all

end module sample_flux_kernel_mod_test
