!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Algorithm to test the automatic code gen of a number of functions 

module boundary_test_alg_mod
  use constants_mod,                     only: r_def, i_def
  use field_mod,                         only: field_type
  use log_mod,                           only: log_event,         &
                                               LOG_LEVEL_INFO

  implicit none
  private
  type( field_type ) :: Ax_u, Ax_xi

  public :: boundary_test_alg_init
  public :: boundary_test_alg

contains

  subroutine boundary_test_alg_init(u, xi)
 
    implicit none
    type( field_type ), intent(inout) :: u, xi

    Ax_u  = field_type( vector_space = u%get_function_space() )
    Ax_xi = field_type( vector_space = xi%get_function_space() )
    call log_event( "Boundary test: initialised boundary test algorithm", LOG_LEVEL_INFO )

  end subroutine boundary_test_alg_init

  subroutine boundary_test_alg(u, xi)

    use psykal_lite_mod,          only: invoke_times_field, &
                                        invoke_set_boundary_kernel
    use matrix_vector_kernel_mod, only: matrix_vector_kernel_type
    use runtime_constants_mod,    only: get_mass_matrix, w2_id, w1_id
    use operator_mod,             only: operator_type

    implicit none
    type( field_type ), intent( inout ) :: u, xi

    type(operator_type), pointer :: mm_w2 => null(), mm_w1 => null()
    real(r_def), parameter       :: bc_value = 3.0_r_def

    ! Allocate and initialise u and xi related fields
    call invoke( set_field_scalar(0.0_r_def, u),    &
                 set_field_scalar(0.0_r_def, Ax_u), &
                 set_field_scalar(0.0_r_def, xi),   & 
                 set_field_scalar(0.0_r_def, Ax_xi) )
    ! Set BC and calculate matrices
    mm_w2 => get_mass_matrix(w2_id)
    call invoke_set_boundary_kernel(u, bc_value)
    call invoke( matrix_vector_kernel_type(Ax_u, u, mm_w2) )

    mm_w1 => get_mass_matrix(w1_id)
    call invoke_set_boundary_kernel(xi, bc_value)
    call invoke( matrix_vector_kernel_type(Ax_xi, xi, mm_w1) )

    ! Ax_u and Ax_xi now only contain non-zero terms for boundary dofs and
    ! for dofs whose domain overlaps boundary dofs. To test the boundary
    ! conditions it is necessary to zero the non-boundary dof terms.
    ! This is achieved by multiplying by u or xi (which are non-zero only 
    ! on the boundary)
    call invoke_times_field(Ax_u,  u,  u)
    call invoke_times_field(Ax_xi, xi, xi)

    call log_event( "Boundary test: finished boundary test algorithm", LOG_LEVEL_INFO )

  end subroutine boundary_test_alg

end module boundary_test_alg_mod
