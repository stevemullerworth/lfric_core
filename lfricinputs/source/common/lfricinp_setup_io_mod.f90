! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file LICENCE
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************
MODULE lfricinp_setup_io_mod

USE clock_mod,                     ONLY: clock_type
USE constants_mod,                 ONLY: i_def, str_max_filename
USE log_mod,                       ONLY: log_event, log_scratch_space,         &
                                         LOG_LEVEL_INFO, LOG_LEVEL_ERROR
USE lfric_xios_file_mod,           ONLY: xios_file_type,                       &
                                         append_file_to_list

IMPLICIT NONE

INTEGER, PARAMETER              :: max_number_ancfiles = 20
CHARACTER(LEN=str_max_filename) :: checkpoint_read_file  = 'unset'
CHARACTER(LEN=str_max_filename) :: checkpoint_write_file = 'unset'
CHARACTER(LEN=str_max_filename) :: ancil_file_map(max_number_ancfiles) = 'unset'
LOGICAL :: checkpoint_write, checkpoint_read, ancil_read

PRIVATE
PUBLIC :: checkpoint_write_file, checkpoint_read_file, ancil_file_map,         &
          init_io_setup, init_lfricinp_files

CONTAINS

SUBROUTINE init_io_setup(io_nl)

USE lfricinp_unit_handler_mod, ONLY: get_free_unit

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: io_nl

CHARACTER(LEN=512) :: message = 'No namelist read'
INTEGER            :: unit_number
INTEGER            :: status = -1

NAMELIST /iofiles/ checkpoint_read,                                            &
                   checkpoint_write,                                           &
                   ancil_read,                                                 &
                   checkpoint_read_file,                                       &
                   checkpoint_write_file,                                      &
                   ancil_file_map

CALL get_free_unit(unit_number)

OPEN(UNIT=unit_number, FILE=io_nl, IOSTAT=status, IOMSG=message)
IF (status /= 0) CALL log_event(message, LOG_LEVEL_ERROR)

READ(unit_number, NML=iofiles, IOSTAT=status, IOMSG=message)
IF (status /= 0) CALL log_event(message, LOG_LEVEL_ERROR)

CLOSE(unit_number)

END SUBROUTINE init_io_setup


SUBROUTINE init_lfricinp_files(files_list, clock)

IMPLICIT NONE

TYPE(xios_file_type), ALLOCATABLE, INTENT(OUT) :: files_list(:)
CLASS(clock_type),               INTENT(IN)    :: clock

TYPE(xios_file_type)                   :: tmp_file

INTEGER, PARAMETER                     :: checkpoint_frequency = 1
CHARACTER(LEN=str_max_filename)        :: ancil_xios_file_id,                  &
                                          ancil_file_path,                     &
                                          afm
INTEGER                                :: split_idx, i

WRITE(log_scratch_space, *) 'Setting up file list. Current clock step is: ',   &
                            clock % get_step()
CALL log_event(log_scratch_space, LOG_LEVEL_INFO)

IF (ancil_read) THEN
  ! Set ancil file reading context informatio for all required ancil files
  DO i = 1, max_number_ancfiles
    ! Exit loop if entry in acil file map is unset
    afm = ancil_file_map(i)
    IF( TRIM(afm) == 'unset') EXIT
    ! From ancil file map string extract ancil xios file id and ancil file path
    split_idx = INDEX(afm, ':')
    ancil_xios_file_id = afm(1:split_idx-1)
    ancil_file_path = afm(split_idx+1:)
    ! Initial ancil file and insert file in file list
    CALL tmp_file%init_xios_file(TRIM(ancil_xios_file_id),                     &
                               path=TRIM(ancil_file_path))
    CALL append_file_to_list(tmp_file, files_list)
  END DO
END IF

! Setup checkpoint writing context information
IF (checkpoint_write) THEN
  ! Create checkpoint filename from stem and first timestep
  CALL tmp_file%init_xios_file("lfric_checkpoint_write",                       &
                               path=TRIM(checkpoint_write_file),               &
                               freq=checkpoint_frequency)
  CALL append_file_to_list(tmp_file, files_list)
END IF

! Setup checkpoint reading context information
IF (checkpoint_read) THEN
  ! Create checkpoint filename from stem
  CALL tmp_file%init_xios_file("lfric_checkpoint_read",                       &
                               path=TRIM(checkpoint_read_file))
  CALL append_file_to_list(tmp_file, files_list)
END IF

END SUBROUTINE init_lfricinp_files

END MODULE lfricinp_setup_io_mod
