!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the field representation
!>
module field_mod_test

  use field_mod,               only : field_type,      &
                                      field_proxy_type
  use function_space_mod,      only : function_space_type, V0
  use constants_mod,           only : r_def, i_def
  use mesh_mod,                only : num_cells, v_unique_dofs, num_layers
  use gaussian_quadrature_mod, only : gaussian_quadrature_type, &
                                      ngp_h, ngp_v, GQ3
  use basis_function_mod,      only : v0_basis, &
                                      v0_diff_basis, &
                                      v0_nodal_coords
  use dofmap_mod,              only : v0_dofmap
  use pFUnit_Mod

  implicit none 
  private
  public  construct_field_test, test_field_data, test_half_field

  @testCase(constructor=construct_field_test)
  type, public, extends( TestCase ) :: field_test_type
  contains

    procedure :: test_field_data
    procedure :: test_half_field

  end type field_test_type

contains

  function construct_field_test() result( new_field_test )
    implicit none

    type( field_test_type ) :: new_field_test

    integer( i_def ) :: num_dofs, num_unique_dofs
    integer( i_def ) :: i

    num_layers=50
    num_cells = 35
    num_dofs=4
    num_unique_dofs = 100
    do i = 1, 4
      v_unique_dofs(i,1)=num_unique_dofs
      v_unique_dofs(i,2)=num_dofs
    enddo

    if ( allocated(v0_basis) ) then
      deallocate( v0_basis )
      deallocate( v0_diff_basis )
      deallocate( v0_nodal_coords )
      deallocate( v0_dofmap )
    end if

    allocate(v0_basis(1,v_unique_dofs(1,2),ngp_h,ngp_v))
    allocate(v0_diff_basis(3,v_unique_dofs(1,2),ngp_h,ngp_v))
    allocate(v0_nodal_coords(3,v_unique_dofs(1,2)))
    allocate( v0_dofmap(0:num_cells,v_unique_dofs(1,2)) )

    v0_basis=sqrt(2.0)
    v0_diff_basis=atan(4.0)
    v0_nodal_coords(1,:)=0.152689632_r_def
    v0_nodal_coords(2,:)=0.562554751_r_def
    v0_nodal_coords(3,:)=-0.8234_r_def
    do i=0,num_cells
      v0_dofmap(i,:) = i+64
    end do

  end function construct_field_test

  !> Ensure constructor and getters work.
  !>
  @test
  subroutine test_field_data( this )

    implicit none

    class( field_test_type ), intent( inout ) :: this

    type( function_space_type )      :: func_space
    type( field_type )               :: f_field
    type( field_proxy_type )         :: f_field_proxy
    type( gaussian_quadrature_type ) :: gaussian_quadrature
    integer                          :: testncell,       &
                                        testnlayers
    integer :: fs, gq

    f_field = field_type( vector_space = func_space%get_instance(V0), &
                          gq = gaussian_quadrature%get_instance(GQ3) )

    f_field_proxy = f_field % get_proxy( )

    ! test the procedures of the type
    testncell = f_field_proxy%vspace%get_ncell()
    @assertEqual( num_cells, testncell )

    testnlayers = f_field_proxy%vspace%get_nlayers()
    @assertEqual( num_layers, testnlayers )

    fs = f_field%which_function_space()
    @assertEqual(fs,V0)

    gq = f_field%which_gaussian_quadrature()
    @assertEqual(gq,GQ3)

  end subroutine test_field_data

  !> Ensure constructor without gaussian quadrature works
  !>
  @test
  subroutine test_half_field( this )

    use field_mod,          only: field_type, field_proxy_type
    use function_space_mod, only: function_space_type, V0

    implicit none

    class( field_test_type ), intent( inout ) :: this

    type( function_space_type ) :: function_space
    type( field_type )          :: test_field
    type( field_proxy_type )    :: test_field_accessor
    type( gaussian_quadrature_type ) :: gaussian_quadrature

    test_field          = field_type( function_space%get_instance( V0 ), &
                          gq = gaussian_quadrature%get_instance(GQ3) )
    test_field_accessor = test_field%get_proxy()

    ! test the procedures of the type
    @assertEqual( test_field_accessor%vspace%get_ncell(), num_cells )

    @assertEqual( test_field_accessor%vspace%get_nlayers(), num_layers )

    @assertEqual( test_field%which_function_space(), V0 )

    @assertEqual( test_field%which_gaussian_quadrature(), GQ3 )

  end subroutine test_half_field

end module field_mod_test
