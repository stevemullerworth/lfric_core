!-----------------------------------------------------------------------------
! (c) Crown copyright 2024 Met Ofine_fieldice. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> Test for the multidata scalar recovery kernel
module prolong_multidata_linear_kernel_mod_test
  use funit
  use constants_mod, only : i_def, r_def

  implicit none
  private :: set_up_unit_test
  public  :: test_masked
  public  :: test_not_masked

contains

subroutine set_up_unit_test( nlayers, ncell_fine_per_coarse_x, ncell_fine_per_coarse_y, ncell_fine, ncell_coarse, &
                             ndf, ndf_coarse_mask, undf_fine, undf_coarse, undf_coarse_mask,                      &
                             stencil_size_wt, stencil_size_w3, ndata, cell_map, map_fine, map_coarse,             &
                             map_coarse_mask, smap_wt, smap_w3, coarse_field, coarse_mask, fine_field,            &
                             answer)
    use get_unit_test_m3x3_dofmap_mod,    only : get_wtheta_m3x3_dofmap, &
                                                 get_w3_m3x3_dofmap
    implicit none

    ! Scalars
    integer(kind=i_def), intent(inout) :: nlayers
    integer(kind=i_def), intent(inout) :: ncell_fine_per_coarse_x ! number of fine cells per coarse in x
    integer(kind=i_def), intent(inout) :: ncell_fine_per_coarse_y ! number of fine cells per coarse in y
    integer(kind=i_def), intent(inout) :: ncell_fine              ! number of fine cells on base mesh
    integer(kind=i_def), intent(inout) :: ncell_coarse            ! number of coarse cells on base mesh
    integer(kind=i_def), intent(inout) :: ndf                     ! number of dofs per cell. Same on each mesh
    integer(kind=i_def), intent(inout) :: ndf_coarse_mask         ! number of dofs per cell for 2D mask
    integer(kind=i_def), intent(inout) :: undf_fine, undf_coarse, undf_coarse_mask
    integer(kind=i_def), intent(inout) :: stencil_size_wt
    integer(kind=i_def), intent(inout) :: stencil_size_w3
    integer(kind=i_def), intent(inout) :: ndata           ! number of fields in multidata field
    ! Integer Arrays
    integer(kind=i_def), allocatable, dimension(:,:,:), intent(inout) :: cell_map
    integer(kind=i_def), allocatable, dimension(:,:), intent(inout)   :: map_fine   ! Cell and dof indexed
    integer(kind=i_def), allocatable, dimension(:,:), intent(inout)   :: map_coarse ! only dof indexed
    integer(kind=i_def), allocatable, dimension(:,:), intent(inout)   :: map_coarse_mask ! only dof indexed
    integer(kind=i_def), allocatable, dimension(:,:), intent(inout)   :: smap_wt
    integer(kind=i_def), allocatable, dimension(:,:), intent(inout)   :: smap_w3
    ! Real Arrays
    real(kind=r_def), allocatable, dimension(:), intent(inout) :: coarse_field, coarse_mask, fine_field, answer
    ! Internal use integers
    integer(kind=i_def) :: i, j, k, k_start

    ! set the scalars by hand
    nlayers = 3
    stencil_size_wt = 5
    stencil_size_w3 = 5
    ncell_fine_per_coarse_x = 3
    ncell_fine_per_coarse_y = 2
    ncell_coarse = 9_i_def
    ncell_fine = ncell_coarse * ncell_fine_per_coarse_x * ncell_fine_per_coarse_y
    ndata = 2

    ! Things for Wtheta
    ndf = 2
    undf_fine = ndata*(nlayers + 1) * ncell_fine
    undf_coarse = ndata*(nlayers + 1) * ncell_coarse

    ! Things for mask
    ndf_coarse_mask = 1
    undf_coarse_mask = ncell_coarse

    allocate( cell_map(ncell_coarse,                      &
                       ncell_fine_per_coarse_x,           &
                       ncell_fine_per_coarse_y),          &
              smap_wt(ndf, stencil_size_wt ),             &
              smap_w3(ndf_coarse_mask, stencil_size_w3 ), &
              map_fine(ndf, ncell_fine),                  &
              map_coarse(ndf, ncell_coarse),              &
              map_coarse_mask(ndf_coarse_mask, ncell_coarse) )
    allocate( coarse_field(undf_coarse),           &
              coarse_mask(undf_coarse_mask),       &
              fine_field(undf_fine),               &
              answer(undf_fine) )

    ! ------------------------------------------------------------------------ !
    ! Make various maps
    ! ------------------------------------------------------------------------ !

    cell_map = reshape( (/  1,  4,  7, 19, 22, 27, 37, 40, 43, &
                            2,  5,  8, 20, 23, 28, 38, 41, 44, &
                            3,  6,  9, 21, 24, 29, 39, 42, 45, &
                           10, 13, 16, 28, 31, 34, 46, 49, 52, &
                           11, 14, 17, 29, 32, 35, 47, 50, 53, &
                           12, 15, 18, 30, 33, 36, 48, 51, 54  &
                          /), (/ ncell_coarse, ncell_fine_per_coarse_x, ncell_fine_per_coarse_y /) )

    call get_w3_m3x3_dofmap(map_coarse_mask, 1)

    do j = 1, ncell_fine
      map_fine(1,j) = (j-1)*(ndata*(nlayers+1))+1
      map_fine(2,j) = (j-1)*(ndata*(nlayers+1))+2
    end do

    do j = 1, ncell_coarse
      map_coarse(1,j) = (j-1)*(ndata*(nlayers+1))+1
      map_coarse(2,j) = (j-1)*(ndata*(nlayers+1))+2
    end do

    ! Only do stencil map for central cell of the 3x3 grid
    smap_wt = reshape( (/ map_coarse(1,5),  map_coarse(2,5), map_coarse(1,4), &
                          map_coarse(2,4),map_coarse(1,8), map_coarse(2,8),  &
                          map_coarse(1,6), map_coarse(2,6), map_coarse(1,2),  &
                          map_coarse(2,2) /), &
                      (/ ndf, stencil_size_wt /) )
    smap_w3 = reshape( (/ map_coarse_mask(1,5), map_coarse_mask(1,4), &
                          map_coarse_mask(1,8), map_coarse_mask(1,6), &
                          map_coarse_mask(1,2) /), &
                       (/ ndf_coarse_mask, stencil_size_w3 /) )

    ! ------------------------------------------------------------------------ !
    ! Set field values
    ! ------------------------------------------------------------------------ !
    fine_field(:) = -99.0_r_def

    ! Make coarse field vary linearly in x, y and z
    ! Cells are numbered as follows:
    ! |-----------------------!
    ! |   1   |   2   |   3   |
    ! |-----------------------|
    ! |   4   |   5   |   6   |
    ! |-----------------------|
    ! |   7   |   8   |   9   |
    ! |-----------------------|

    ! Cells 1, 3, 7, 9 don't feature with the cross stencil
    coarse_field(:) = 100.0_r_def

    do i = 1, ndata
      k_start = (i-1)*(nlayers - 1 + ndf)+1
      do k = 0, nlayers
        coarse_field(map_coarse(1,2)+k+k_start-1) = 9.0_r_def + real(k,r_def)
        coarse_field(map_coarse(1,4)+k+k_start-1) = 5.0_r_def + real(k,r_def)
        coarse_field(map_coarse(1,5)+k+k_start-1) = 11.0_r_def + real(k,r_def)
        coarse_field(map_coarse(1,6)+k+k_start-1) = 17.0_r_def + real(k,r_def)
        coarse_field(map_coarse(1,8)+k+k_start-1) = 13.0_r_def + real(k,r_def)
      end do
    end do

  end subroutine set_up_unit_test

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  ! Test with mask
  @test ( )
  subroutine test_masked( )
    use sci_prolong_multidata_linear_kernel_mod, only : prolong_multidata_linear_kernel_code
    use get_unit_test_m3x3_dofmap_mod,    only : get_wtheta_m3x3_dofmap, &
                                                 get_w3_m3x3_dofmap
    implicit none

    integer(kind=i_def) :: i, k, cell, k_start
    ! Scalars
    integer(kind=i_def) :: nlayers
    integer(kind=i_def) :: ncell_fine_per_coarse_x ! number of fine cells per coarse in x
    integer(kind=i_def) :: ncell_fine_per_coarse_y ! number of fine cells per coarse in y
    integer(kind=i_def) :: ncell_fine              ! number of fine cells on base mesh
    integer(kind=i_def) :: ncell_coarse            ! number of coarse cells on base mesh
    integer(kind=i_def) :: ndf                     ! number of dofs per cell. Same on each mesh
    integer(kind=i_def) :: ndf_coarse_mask         ! number of dofs per cell for 2D mask
    integer(kind=i_def) :: undf_fine, undf_coarse, undf_coarse_mask
    integer(kind=i_def) :: stencil_size_wt
    integer(kind=i_def) :: stencil_size_w3
    integer(kind=i_def) :: ndata           ! number of fields in multidata field
    ! Integer Arrays
    integer(kind=i_def), allocatable, dimension(:,:,:) :: cell_map
    integer(kind=i_def), allocatable, dimension(:,:)   :: map_fine   ! Cell and dof indexed
    integer(kind=i_def), allocatable, dimension(:,:)   :: map_coarse ! only dof indexed
    integer(kind=i_def), allocatable, dimension(:,:)   :: map_coarse_mask ! only dof indexed
    integer(kind=i_def), allocatable, dimension(:,:)   :: smap_wt
    integer(kind=i_def), allocatable, dimension(:,:)   :: smap_w3
    ! Real Arrays
    real(kind=r_def), allocatable, dimension(:) :: coarse_field, coarse_mask, fine_field, answer

    real(r_def), parameter :: tol = 1.0e-12_r_def

    ! Set up everything for unit test
    call set_up_unit_test( nlayers, ncell_fine_per_coarse_x, ncell_fine_per_coarse_y, ncell_fine, ncell_coarse, &
                          ndf, ndf_coarse_mask, undf_fine, undf_coarse, undf_coarse_mask,                       &
                          stencil_size_wt, stencil_size_w3, ndata, cell_map, map_fine, map_coarse,              &
                          map_coarse_mask, smap_wt, smap_w3, coarse_field, coarse_mask, fine_field,             &
                          answer)

    ! Coarse mask is always 1 - should return linear remapping
    coarse_mask(:) = 1.0_r_def

    do i = 1, ndata
      k_start = (i-1)*(nlayers - 1 + ndf)+1
      do k = 0, nlayers
        ! True values for central cell
        answer(map_fine(1,22)+k+k_start-1) = 8.5_r_def + real(k,r_def)
        answer(map_fine(1,23)+k+k_start-1) = 10.5_r_def + real(k,r_def)
        answer(map_fine(1,24)+k+k_start-1) = 12.5_r_def + real(k,r_def)
        answer(map_fine(1,31)+k+k_start-1) = 9.5_r_def + real(k,r_def)
        answer(map_fine(1,32)+k+k_start-1) = 11.5_r_def + real(k,r_def)
        answer(map_fine(1,33)+k+k_start-1) = 13.5_r_def + real(k,r_def)
      end do
    end do

    ! ------------------------------------------------------------------------ !
    ! Run kernel
    ! ------------------------------------------------------------------------ !
    ! This should return a linear reconstruction
    cell = 5

    call prolong_multidata_linear_kernel_code( nlayers,              &
                                            cell_map(cell,:,:),      &
                                            ncell_fine_per_coarse_x, &
                                            ncell_fine_per_coarse_y, &
                                            ncell_fine,              &
                                            fine_field,              &
                                            coarse_field,            &
                                            stencil_size_wt,         &
                                            smap_wt,                 &
                                            coarse_mask,             &
                                            stencil_size_w3,         &
                                            smap_w3,                 &
                                            ndata,                   &
                                            ndf,                     &
                                            undf_fine,               &
                                            map_fine,                &
                                            undf_coarse,             &
                                            map_coarse(:,cell),      &
                                            ndf_coarse_mask,         &
                                            undf_coarse_mask,        &
                                            map_coarse_mask(:,cell) )

    ! Check fine field values in central cell (5) match the expected answer
    do i = 1, ndata
      k_start = (i-1)*(nlayers - 1 + ndf)+1
      do k = 0, nlayers
        @assertEqual( answer(map_fine(1,22)+k+k_start-1), fine_field(map_fine(1,22)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,23)+k+k_start-1), fine_field(map_fine(1,23)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,24)+k+k_start-1), fine_field(map_fine(1,24)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,31)+k+k_start-1), fine_field(map_fine(1,31)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,32)+k+k_start-1), fine_field(map_fine(1,32)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,33)+k+k_start-1), fine_field(map_fine(1,33)+k+k_start-1), tol)
      end do
    end do

    deallocate( cell_map,        &
                answer,          &
                map_fine,        &
                map_coarse,      &
                map_coarse_mask, &
                coarse_field,    &
                coarse_mask,     &
                smap_wt,         &
                fine_field )

  end subroutine test_masked

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  ! Test without mask
  @test ( )
  subroutine test_not_masked( )
    use sci_prolong_multidata_linear_kernel_mod, only : prolong_multidata_linear_kernel_code

    integer(kind=i_def) :: i, k, cell, k_start
    ! Scalars
    integer(kind=i_def) :: nlayers
    integer(kind=i_def) :: ncell_fine_per_coarse_x ! number of fine cells per coarse in x
    integer(kind=i_def) :: ncell_fine_per_coarse_y ! number of fine cells per coarse in y
    integer(kind=i_def) :: ncell_fine              ! number of fine cells on base mesh
    integer(kind=i_def) :: ncell_coarse            ! number of coarse cells on base mesh
    integer(kind=i_def) :: ndf                     ! number of dofs per cell. Same on each mesh
    integer(kind=i_def) :: ndf_coarse_mask         ! number of dofs per cell for 2D mask
    integer(kind=i_def) :: undf_fine, undf_coarse, undf_coarse_mask
    integer(kind=i_def) :: stencil_size_wt
    integer(kind=i_def) :: stencil_size_w3
    integer(kind=i_def) :: ndata           ! number of fields in multidata field
    ! Integer Arrays
    integer(kind=i_def), allocatable, dimension(:,:,:) :: cell_map
    integer(kind=i_def), allocatable, dimension(:,:)   :: map_fine   ! Cell and dof indexed
    integer(kind=i_def), allocatable, dimension(:,:)   :: map_coarse ! only dof indexed
    integer(kind=i_def), allocatable, dimension(:,:)   :: map_coarse_mask ! only dof indexed
    integer(kind=i_def), allocatable, dimension(:,:)   :: smap_wt
    integer(kind=i_def), allocatable, dimension(:,:)   :: smap_w3
    ! Real Arrays
    real(kind=r_def), allocatable, dimension(:) :: coarse_field, coarse_mask, fine_field, answer

    real(r_def), parameter :: tol = 1.0e-12_r_def

    ! Set up everything for unit test
    call set_up_unit_test( nlayers, ncell_fine_per_coarse_x, ncell_fine_per_coarse_y, ncell_fine, ncell_coarse, &
                          ndf, ndf_coarse_mask, undf_fine, undf_coarse, undf_coarse_mask,                       &
                          stencil_size_wt, stencil_size_w3, ndata, cell_map, map_fine, map_coarse,              &
                          map_coarse_mask, smap_wt, smap_w3, coarse_field, coarse_mask, fine_field,             &
                          answer)

    ! Coarse mask is 0 - should return a constant mapping
    coarse_mask(:) = 0.0_r_def
    do i = 1, ndata
      k_start = (i-1)*(nlayers - 1 + ndf)+1
      do k = 0, nlayers
        ! True values for central cell
        answer(map_fine(1,22)+k+k_start-1) = coarse_field(map_coarse(1,5)+k+k_start-1)
        answer(map_fine(1,23)+k+k_start-1) = coarse_field(map_coarse(1,5)+k+k_start-1)
        answer(map_fine(1,24)+k+k_start-1) = coarse_field(map_coarse(1,5)+k+k_start-1)
        answer(map_fine(1,31)+k+k_start-1) = coarse_field(map_coarse(1,5)+k+k_start-1)
        answer(map_fine(1,32)+k+k_start-1) = coarse_field(map_coarse(1,5)+k+k_start-1)
        answer(map_fine(1,33)+k+k_start-1) = coarse_field(map_coarse(1,5)+k+k_start-1)
      end do
    end do

    ! ------------------------------------------------------------------------ !
    ! Run kernel
    ! ------------------------------------------------------------------------ !
    ! This should return a constant mapping
    cell = 5

    call prolong_multidata_linear_kernel_code( nlayers,              &
                                            cell_map(cell,:,:),      &
                                            ncell_fine_per_coarse_x, &
                                            ncell_fine_per_coarse_y, &
                                            ncell_fine,              &
                                            fine_field,              &
                                            coarse_field,            &
                                            stencil_size_wt,         &
                                            smap_wt,                 &
                                            coarse_mask,             &
                                            stencil_size_w3,         &
                                            smap_w3,                 &
                                            ndata,                   &
                                            ndf,                     &
                                            undf_fine,               &
                                            map_fine,                &
                                            undf_coarse,             &
                                            map_coarse(:,cell),      &
                                            ndf_coarse_mask,         &
                                            undf_coarse_mask,        &
                                            map_coarse_mask(:,cell) )

    ! Check fine field values in central cell (5) match the expected answer
    do i = 1, ndata
      k_start = (i-1)*(nlayers - 1 + ndf)+1
      do k = 0, nlayers
        @assertEqual( answer(map_fine(1,22)+k+k_start-1), fine_field(map_fine(1,22)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,23)+k+k_start-1), fine_field(map_fine(1,23)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,24)+k+k_start-1), fine_field(map_fine(1,24)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,31)+k+k_start-1), fine_field(map_fine(1,31)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,32)+k+k_start-1), fine_field(map_fine(1,32)+k+k_start-1), tol)
        @assertEqual( answer(map_fine(1,33)+k+k_start-1), fine_field(map_fine(1,33)+k+k_start-1), tol)
      end do
    end do
    deallocate( cell_map,        &
                answer,          &
                map_fine,        &
                map_coarse,      &
                map_coarse_mask, &
                coarse_field,    &
                coarse_mask,     &
                smap_wt,         &
                fine_field )

  end subroutine test_not_masked

end module prolong_multidata_linear_kernel_mod_test
