!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! For further details please refer to the file LICENCE which you should have
! received as part of this distribution.
!-----------------------------------------------------------------------------
!>@brief Application of the Helmholtz operator in the gravity wave system
!>
!>@details The Helmholtz operator is given by
!>
!>   \f[ H = Id + \alpha^2(\Delta t)^2 c_s^2 M_3^{-1} D H_B^{-1} D^T\f]
!>
!> with the lumped matrix \f$H_B\f$ defined by
!>
!> \f[ H_B=\tilde{M}_2+\alpha^2(\Delta t)^2N^2\tilde{Q \tilde{M}_b^{-1}Q^T} \f]
!>
!> This module provides a type derived from the abstract operator type which
!> allows application of this Helmholtz operator.

module gw_pressure_operator_alg_mod
  use hierarchical_linear_operator_mod,   only: abstract_hierarchical_linear_operator_type
  use vector_mod,                         only: abstract_vector_type
  use field_mod,                          only: field_type
  use field_vector_mod,                   only: field_vector_type
  use constants_mod,                      only: i_def, r_def
  use fem_constants_mod,                  only: get_mass_matrix,     &
                                                get_inverse_w3_mass_matrix, &
                                                get_div
  use fs_continuity_mod,                  only: W0, W2, W3, Wtheta
  use timestepping_config_mod,            only: dt

  use initial_temperature_config_mod,     only: bvf_square

  use operator_mod,                       only: operator_type
  use field_indices_mod,                  only: igw_u, igw_p, igw_b
  use log_mod,                            only: log_event,       &
                                                LOG_LEVEL_ERROR, &
                                                LOG_LEVEL_INFO,  &
                                                log_scratch_space
  use mesh_mod,                           only: mesh_type
  use gravity_wave_constants_config_mod,  only: cs_square,  &
                                                b_space,    &
                                                b_space_w0, &
                                                b_space_w3, &
                                                b_space_wtheta
  use function_space_chain_mod,           only: multigrid_function_space_chain

  implicit none

  type, public, extends(abstract_hierarchical_linear_operator_type) :: &
                        gw_pressure_operator_type
  private
     !> Off-centering parameter in SI timestepping
     real(kind=r_def) :: alpha
     !> Temporary fields used in operator application
     type(field_type) :: grad_p, div_grad_p
     !> Lumped inverse \f$\tilde{H}_B^{-1}\f$
     type(field_type) :: Hb_lumped_inv
     !> Multigrid level
     integer(kind=i_def) :: level
     !> Id of pressure function space
     integer(kind=i_def) :: fs_id
     contains
     !> Return the lumped inverse \f$\tilde{H}_B^{-1}\f$
     procedure, public  :: get_Hb_lumped_inv
     !> over-ride the abstract interface
     !> param[in] self a linear operator
     !> param[in] x a fieldvector the linear operator is applied to
     !> param[inout] y a fieldvector, the result.
     procedure, public  :: apply => apply_gw_pressure_operator
     procedure, private :: apply_gw_pressure_operator
     !> performs a deep copy (for overriding the "=" operator)
     !> @param[out] dest   gw_pressure_operator_type lhs
     !> @param[in]  source gw_pressure_operator_type rhs
     procedure, public  :: gw_pressure_operator_assign
     procedure, public  :: coarsen => coarsen_gw_pressure_operator
     procedure, private :: coarsen_gw_pressure_operator

     final              :: destroy_gw_pressure_operator

     !> Override default assignment for gw_pressure_operator_type pairs.
     generic            :: assignment(=) => gw_pressure_operator_assign

  end type gw_pressure_operator_type

  interface gw_pressure_operator_type
     module procedure gw_pressure_operator_constructor
  end interface

contains

  !>@brief Construct new instance of type
  !>
  !>@details Extract function spaces and build the lumped approximate
  !>         inverse \f$ \tilde{H}_b^{-1} \f$ as well as other operators
  !>         such as div and grad for the Helmholtz operator application.
  !>
  !>@param[in] wp Function space for the pressure on this level
  !>@param[in] level Multigrid level
  !>@return instance of the Helmholtz operator
  function gw_pressure_operator_constructor(wp, level) result(self)
    use function_space_mod,            only: function_space_type
    use fs_continuity_mod,             only: W2, W0, W3, Wtheta
    use finite_element_config_mod,     only: element_order
    use quadrature_xyoz_mod,           only: quadrature_xyoz_type
    use quadrature_rule_gaussian_mod,  only: quadrature_rule_gaussian_type
    use compute_q_operator_kernel_mod, only: compute_q_operator_type
    use matrix_vector_kernel_mod,      only: matrix_vector_kernel_type
    use transpose_matrix_vector_kernel_mod, only: transpose_matrix_vector_kernel_type
    use function_space_collection_mod, only: function_space_collection

    implicit none

    type(gw_pressure_operator_type)     :: self
    type(function_space_type), pointer, intent(in) :: wp
    integer(kind=i_def), intent(in)     :: level
    type(function_space_type), pointer  :: wu => null(), &
                                           wb => null()
    integer(i_def)                      :: order
    type(mesh_type), pointer            :: mesh => null()
    real(kind=r_def)                    :: const1
    type(field_type)                    :: ones, M_lumped, &
                                           ones_u, M_lumped_u, &
                                           Mb_lumped_inv, QT1
    type(operator_type), pointer        :: Mb => null(), Mu => null()
    type(quadrature_xyoz_type)          :: qr
    type(quadrature_rule_gaussian_type) :: quadrature_rule
    type(operator_type)                 :: Q


    call log_event( 'Constructing pressure operator...', LOG_LEVEL_INFO )

    self%alpha = 0.5_r_def
    self%level = level

    ! Id of pressure function space
    self%fs_id = wp%get_id()

    ! Get needed function spaces
    order =  wp%get_element_order()
    mesh  => wp%get_mesh()

    wu => function_space_collection%get_fs( mesh, order, W2 )
    select case(b_space)
      case(b_space_w0)
         wb => function_space_collection%get_fs( mesh, order, W0 )
      case(b_space_w3)
         wb => function_space_collection%get_fs( mesh, order, W3 )
      case(b_space_wtheta)
         wb => function_space_collection%get_fs( mesh, order, Wtheta )
    end select

    ! Temporaries required in operator application
    call self%grad_p%initialise( vector_space = wu )
    call self%div_grad_p%initialise( vector_space = wp )

    ! Compute inverse approximate \tilde{M}_b^{-1}
    call ones%initialise( vector_space = wb )
    call M_lumped%initialise( vector_space = wb )
    call Mb_lumped_inv%initialise( vector_space = wb )
    select case(b_space)
      case(b_space_w0)
        Mb => get_mass_matrix(W0, mesh%get_id())
      case(b_space_w3)
        Mb => get_mass_matrix(W3, mesh%get_id())
      case(b_space_wtheta)
        Mb => get_mass_matrix(Wtheta, mesh%get_id())
    end select
    call invoke( setval_c( ones,     1.0_r_def ),               &
                 setval_c( M_lumped, 0.0_r_def ),               &
                 matrix_vector_kernel_type(M_lumped, ones, Mb), &
                 X_divideby_Y(Mb_lumped_inv, ones, M_lumped) )

    ! Compute Q (projection) operator
    call Q%initialise( wu, wb )
    qr = quadrature_xyoz_type(element_order+2, quadrature_rule)
    call invoke( compute_q_operator_type(Q, qr) )

   ! Compute inverse approximate \f$ \tilde{H}_b^{-1} \f$
    call ones_u%initialise( vector_space = wu )
    call M_lumped_u%initialise( vector_space = wu )
    call QT1%initialise( vector_space = wb )
    call self%Hb_lumped_inv%initialise( vector_space = wu )
    Mu => get_mass_matrix(W2, mesh%get_id())

    const1 = self%alpha**2*dt**2*bvf_square
    call invoke( setval_c(ones_u,     1.0_r_def),                        &
                 setval_c(M_lumped_u, 0.0_r_def),                        &
                 setval_c(QT1,        0.0_r_def),                        &
                 transpose_matrix_vector_kernel_type(QT1, ones_u, Q),    &
                 inc_X_times_Y(QT1, Mb_lumped_inv),                      &
                 matrix_vector_kernel_type(M_lumped_u, QT1, Q),          &
                 inc_a_times_X(const1, M_lumped_u),                      &
                 matrix_vector_kernel_type(M_lumped_u, ones_u, Mu),      &
                 X_divideby_Y(self%Hb_lumped_inv, ones_u, M_lumped_u) )


    call log_event( 'done', LOG_LEVEL_INFO )

    nullify(mesh)

  end function gw_pressure_operator_constructor

  !> Performs a deep copy between gw_pressure_operator_type pairs.
  !> (for overriding the "=" operator)
  !>
  !> @param[out] dest   gw_pressure_operator_type lhs
  !> @param[in]  source gw_pressure_operator_type rhs
  subroutine gw_pressure_operator_assign(dest, source)

    implicit none
    class(gw_pressure_operator_type), intent(in)  :: source
    class(gw_pressure_operator_type), intent(out) :: dest

    ! Deep copy of the contents of the pressure_operator_type
    dest%alpha = source%alpha
    dest%level = source%level
    dest%fs_id = source%fs_id
    call source%grad_p%copy_field(dest%grad_p)
    call source%div_grad_p%copy_field(dest%div_grad_p)
    call source%Hb_lumped_inv%copy_field(dest%Hb_lumped_inv)

  end subroutine gw_pressure_operator_assign

  !>@brief Return lumped inverse matrix
  !>
  !>@details Return the matrix \f$\tilde{H}_b^{-1}\f$. This matrix is also
  !> used in the mixed preconditioner and returning it here avoids building
  !> it twice.
  !>
  !>@param [in] self Instance of object
  function get_Hb_lumped_inv(self) result(Hb_lumped_inv)
    implicit none
    class(gw_pressure_operator_type), intent(in) :: self
    type(field_type) :: Hb_lumped_inv
    call self%Hb_lumped_inv%copy_field(Hb_lumped_inv)
  end function get_Hb_lumped_inv

  !>@brief Apply Helmholtz operator to a pressure field to obtain \f$y=Hx\f$
  !>
  !>@param[in] self instance of gw_pressure_operator_type
  !>@param[in] x field \f$x\f$ to apply operator to
  !>@param[inout] y Resulting field \f$y=Hx\f$ in pressure space
  subroutine apply_gw_pressure_operator(self, x, y)
    use dg_matrix_vector_kernel_mod,        only: dg_matrix_vector_kernel_type
    use transpose_matrix_vector_kernel_mod, only: transpose_matrix_vector_kernel_type
    use enforce_bc_kernel_mod,              only: enforce_bc_kernel_type

    implicit none
    class(gw_pressure_operator_type), intent(inout) :: self
    class(abstract_vector_type), intent(in)      :: x
    class(abstract_vector_type), intent(inout)   :: y
    real(kind=r_def)    :: const

    type(mesh_type), pointer :: mesh => null()

    type(operator_type), pointer :: div => null(), M3_inv => null()

    select type (x)
    type is (field_vector_type)
       select type (y)
       type is (field_vector_type)
          mesh   => x%vector(1)%get_mesh()
          div    => get_div(mesh%get_id())
          M3_inv => get_inverse_w3_mass_matrix(mesh%get_id())
          const = self%alpha**2*dt**2*cs_square
          call invoke( setval_c(self%grad_p, 0.0_r_def),                &
                       transpose_matrix_vector_kernel_type(self%grad_p, &
                                                           x%vector(1), &
                                                           div),        &
                       enforce_bc_kernel_type(self%grad_p),             &
                       inc_X_times_Y(self%grad_p, self%Hb_lumped_inv),  &
                       dg_matrix_vector_kernel_type(self%div_grad_p,    &
                                                    self%grad_p,        &
                                                    div),               &
                       dg_matrix_vector_kernel_type(y%vector(1),        &
                                                    self%div_grad_p,    &
                                                    M3_inv),            &
                       inc_aX_plus_Y(const, y%vector(1), x%vector(1)) )


          nullify( div, M3_inv )
       class default
          write(log_scratch_space, '(A)') &
               "gw_pressure_operator_alg_mod: incorrect vector_type argument y"
          call log_event(log_scratch_space, LOG_LEVEL_ERROR)
       end select
    class default
       write(log_scratch_space, '(A)') &
             "gw_pressure_operator_alg_mod: incorrect vector_type argument x"
       call log_event(log_scratch_space, LOG_LEVEL_ERROR)
    end select

    nullify( mesh )

  end subroutine apply_gw_pressure_operator

  !> Return a coarsened version of the operator on the
  !> next level of the multigrid hierarchy.
  !>
  !>@param[in] self instance of gw_pressure_operator_type
  !>@param[in] fs_coarse coarse level function space
  !>@param[inout] coarse_operator coarsened version of the operator on the next multigrid level
  subroutine coarsen_gw_pressure_operator(self,fs_coarse,coarse_operator)
    use function_space_mod,            only: function_space_type
    implicit none
    class(gw_pressure_operator_type), intent(in) :: self
    type(function_space_type), pointer, intent(in) :: fs_coarse
    class(abstract_hierarchical_linear_operator_type), allocatable, intent(inout) :: coarse_operator
    allocate(coarse_operator, &
             source=gw_pressure_operator_type(fs_coarse,self%level+1))
  end subroutine coarsen_gw_pressure_operator

  !> Finalizer for the Helmholtz operator
  !> @param [inout] self the pressure operator
  subroutine destroy_gw_pressure_operator(self)
    implicit none
    type(gw_pressure_operator_type), intent(inout) :: self
  end subroutine destroy_gw_pressure_operator

end module gw_pressure_operator_alg_mod
