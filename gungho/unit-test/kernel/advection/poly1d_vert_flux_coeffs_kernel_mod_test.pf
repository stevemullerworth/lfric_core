!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the computation of the vertical flux coefficients
module poly1d_vert_flux_coeffs_kernel_mod_test

  use constants_mod, only: i_def, r_def
  use pFUnit_Mod
  use yaxt,          only: xt_initialize, xt_finalize
  use mpi_mod,       only: store_comm, clear_comm

  implicit none

  private
  public :: test_uniform_mesh
  public :: test_stretched_mesh

  @TestCase
  type, extends(MPITestCase), public :: poly1d_vert_flux_coeffs_test_type
    private
  contains
    procedure setUp
    procedure test_uniform_mesh
    procedure test_stretched_mesh
    procedure tearDown
    procedure test_function
  end type poly1d_vert_flux_coeffs_test_type

contains

  subroutine setUp( this )
    use feign_config_mod,     only: feign_base_mesh_config, &
                                    feign_planet_config
    use base_mesh_config_mod, only: geometry_planar,    &
                                    partitioner_planar, &
                                    topology_fully_periodic
    implicit none

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo',                      &
                                 prime_mesh_name='unit_test',         &
                                 geometry=geometry_planar,            &
                                 partitioner=partitioner_planar,      &
                                 topology=topology_fully_periodic,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

  end subroutine setUp

  @Test( npes=[1] )
  subroutine test_uniform_mesh( this )

    use, intrinsic :: iso_fortran_env,      only: real64
    use poly1d_vert_flux_coeffs_kernel_mod, only: poly1d_vert_flux_coeffs_code
    implicit none

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this

    real(r_def), parameter    :: tol = 1.0e-12_r_def  ! r_def 64bit
    real(r_def), dimension(3) :: answer
    real(r_def)               :: use_tol

    integer(i_def), parameter :: nfaces_v = 2
    integer(i_def), parameter :: nlayers = 8
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: ndf_wx = 8
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers
    integer(i_def), parameter :: undf_wx = ndf_wx*nlayers
    integer(i_def), parameter :: global_order = 2
    integer(i_def), parameter :: nqp_h = 1
    integer(i_def), parameter :: nqp_v = 3
    integer(i_def), parameter :: nqp_f = 1
    integer(i_def), dimension(ndf_w3) :: map_w3
    integer(i_def), dimension(ndf_wx) :: map_wx

    real(r_def), dimension(global_order+1,2,undf_w3) :: coeff
    real(r_def), dimension(undf_w3) :: mdw3
    real(r_def), dimension(undf_wx) :: x, y, z

    real(r_def), dimension(nqp_h) :: wqp_h
    real(r_def), dimension(nqp_v) :: wqp_v
    real(r_def), dimension(nqp_f) :: wqp_f

    real(r_def), dimension(1,ndf_wx,nqp_h,nqp_v) :: basis_wx
    real(r_def), dimension(1,ndf_wx,nqp_f,2)     :: face_basis_wx

    real(r_def), parameter :: dx = 2.0_r_def
    real(r_def), parameter :: dy = 1.7_r_def
    real(r_def)            :: dz = 3.0_r_def

    real(r_def), dimension(3) :: xq

    integer(i_def) :: i, k, df

    xq(:) = 0.5_r_def + 0.5_r_def*(/ -sqrt(3.0_r_def/5.0_r_def), 0.0_r_def, sqrt(3.0_r_def/5.0_r_def) /)
    wqp_v(:) = (/5.0_r_def, 8.0_r_def, 5.0_r_def/)/18.0_r_def

    map_w3(1) = 1

    do df = 1,ndf_wx
      map_wx(df) = 1 + (df-1)*nlayers
    end do

    mdw3(:) = dx*dy*dz

    x(:) = 0.0_r_def
    y(:) = 0.0_r_def
    z(:) = 0.0_r_def
    do k = 0,nlayers-1
      x(map_wx(2)+k) = dx
      x(map_wx(4)+k) = dx
      y(map_wx(3)+k) = dy
      y(map_wx(4)+k) = dy

      do df = 1,4
        x(map_wx(4+df)+k) = x(map_wx(df)+k)
        y(map_wx(4+df)+k) = y(map_wx(df)+k)
        z(map_wx(df)+k)   = k*dz
        z(map_wx(4+df)+k) = z(map_wx(df)+k) + dz
      end do
    end do

    face_basis_wx(:,:,:,:) = 0.0_r_def
    do df = 1,4
      face_basis_wx(1,df,:,1) = 0.25_r_def
      face_basis_wx(1,4+df,:,2) = 0.25_r_def
    end do
    do k = 1,3
      do df = 1,4
        basis_wx(1,df,:,k) = 0.25_r_def*(1.0_r_def - xq(k))
        basis_wx(1,4+df,:,k) = 0.25_r_def*xq(k)
      end do
    end do

    wqp_h(1) = 1.0_r_def
    wqp_f(1) = 1.0_r_def

    call poly1d_vert_flux_coeffs_code(nlayers,                   &
                                      coeff,                     &
                                      mdw3,                      &
                                      x, y, z,                   &
                                      ndf_w3,                    &
                                      undf_w3,                   &
                                      map_w3,                    &
                                      ndf_wx,                    &
                                      undf_wx,                   &
                                      map_wx,                    &
                                      basis_wx,                  &
                                      face_basis_wx,             &
                                      global_order,              &
                                      nfaces_v,                  &
                                      nqp_h, nqp_v, wqp_h, wqp_v,&
                                      nfaces_v, nqp_f, wqp_f )

    if ( r_def == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_def*spacing( 6.0_r_def )
    end if

    answer = (/ 2.0_r_def, 5.0_r_def, -1.0_r_def /)/6.0_r_def
    @assertEqual(answer, coeff(:,1,2), use_tol)

    answer = (/ -1.0_r_def, 5.0_r_def, 2.0_r_def /)/6.0_r_def
    @assertEqual(answer, coeff(:,2,2), use_tol)

    answer = (/ 2.0_r_def, 5.0_r_def, -1.0_r_def /)/6.0_r_def
    @assertEqual(answer, coeff(:,2,1), use_tol)

    answer = (/ -1.0_r_def, 5.0_r_def, 2.0_r_def /)/6.0_r_def
    @assertEqual(answer, coeff(:,1,nlayers), use_tol)

  end subroutine test_uniform_mesh

  @Test( npes=[1] )
  subroutine test_stretched_mesh( this )

    use, intrinsic :: iso_fortran_env,      only: real64
    use poly1d_vert_flux_coeffs_kernel_mod, only: poly1d_vert_flux_coeffs_code
    use poly1d_vert_flux_kernel_mod,        only: poly1d_vert_flux_code
    implicit none

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this

    real(r_def), parameter    :: tol = 1.0e-12_r_def  ! r_def 64bit
    real(r_def)               :: ans, mx, use_tol

    integer(i_def), parameter :: nfaces_v = 2
    integer(i_def), parameter :: nlayers = 8
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: ndf_wx = 8
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers
    integer(i_def), parameter :: undf_wx = ndf_wx*nlayers
    integer(i_def), parameter :: global_order = 2
    integer(i_def), parameter :: nqp_h = 1
    integer(i_def), parameter :: nqp_v = 3
    integer(i_def), parameter :: nqp_f = 1
    integer(i_def), dimension(ndf_w3) :: map_w3
    integer(i_def), dimension(ndf_wx) :: map_wx

    real(r_def), dimension(global_order+1,2,undf_w3) :: coeff
    real(r_def), dimension(undf_w3) :: mdw3
    real(r_def), dimension(undf_wx) :: x, y, z

    real(r_def), dimension(nqp_h) :: wqp_h
    real(r_def), dimension(nqp_v) :: wqp_v
    real(r_def), dimension(nqp_f) :: wqp_f

    real(r_def), dimension(1,ndf_wx,nqp_h,nqp_v) :: basis_wx
    real(r_def), dimension(1,ndf_wx,nqp_f,2)     :: face_basis_wx

    real(r_def), parameter :: dx = 2.0_r_def
    real(r_def), parameter :: dy = 1.7_r_def
    real(r_def)            :: dz = 3.0_r_def

    real(r_def), dimension(3) :: xq

    integer(i_def) :: i, k, df

    integer(i_def), parameter :: ndf_w2 = 6
    integer(i_def), parameter :: undf_w2 = 4*nlayers + 2*(nlayers+1)
    integer(i_def), dimension(ndf_w2)  :: map_w2
    real(r_def),    dimension(undf_w2) :: flux
    real(r_def),    dimension(undf_w2) :: wind
    real(r_def),    dimension(undf_w3) :: density

    real(r_def), dimension(3,ndf_w2,ndf_w2) :: basis
    real(r_def), dimension(3,2)             :: outward_normals
    real(r_def)                             :: div_f, z_w3

    xq(:) = 0.5_r_def + 0.5_r_def*(/ -sqrt(3.0_r_def/5.0_r_def), 0.0_r_def, sqrt(3.0_r_def/5.0_r_def) /)
    wqp_v(:) = (/5.0_r_def, 8.0_r_def, 5.0_r_def/)/18.0_r_def

    map_w3(1) = 1

    do df = 1,ndf_wx
      map_wx(df) = 1 + (df-1)*nlayers
    end do

    face_basis_wx(:,:,:,:) = 0.0_r_def
    do df = 1,4
      face_basis_wx(1,df,:,1) = 0.25_r_def
      face_basis_wx(1,4+df,:,2) = 0.25_r_def
    end do
    do k = 1,3
      do df = 1,4
        basis_wx(1,df,:,k) = 0.25_r_def*(1.0_r_def - xq(k))
        basis_wx(1,4+df,:,k) = 0.25_r_def*xq(k)
      end do
    end do

    wqp_h(1) = 1.0_r_def
    wqp_f(1) = 1.0_r_def


    ! A more sever test is to check if the reconstruction
    ! on a non-uniform grid returns the correct aswer.
    ! We compute the coefficients on a non-uniform
    ! mesh and then use them to compute the field
    ! on the staggered points and compare that with the
    ! exact answer. As a basic test we check that
    ! the reconstruction can exactly reconstruct
    ! a linear field.

    ! Create the w2 basis function array
    ! Nodal points are (x,z,y) = (0,1/2,1/2)
    !                            (1/2,0,1/2)
    !                            (1,0,1/2)
    !                            (1/2,1,1/2)
    !                            (1/2,1/2,0)
    !                            (1/2,1/2,1)
    ! Basis functions are (1-x,0,0)
    !                     (0,-(1-y),0)
    !                     (x,0,0)
    !                     (0,-y,0)
    !                     (0,0,1-z)
    !                     (0,0,z)
    basis = 0.0_r_def
    basis(1,1,:) = 0.5_r_def
    basis(1,1,1) = 1.0_r_def
    basis(1,1,3) = 0.0_r_def
    basis(2,2,:) =-0.5_r_def
    basis(2,2,2) =-1.0_r_def
    basis(2,2,4) = 0.0_r_def
    basis(1,3,:) = 0.5_r_def
    basis(1,3,1) = 0.0_r_def
    basis(1,3,3) = 1.0_r_def
    basis(2,4,:) =-0.5_r_def
    basis(2,4,2) = 0.0_r_def
    basis(2,4,4) = 1.0_r_def
    basis(3,5,:) = 0.5_r_def
    basis(3,5,5) = 1.0_r_def
    basis(3,5,6) = 0.0_r_def
    basis(3,6,:) =-0.5_r_def
    basis(3,6,5) = 0.0_r_def
    basis(3,6,6) = 1.0_r_def

    ! Construct the outward normals
    outward_normals = 0.0_r_def
    outward_normals(3,1) = -1.0_r_def
    outward_normals(3,2) =  1.0_r_def


    coeff = 0.0_r_def
    mdw3 = 0.0_r_def
    x(:) = 0.0_r_def
    y(:) = 0.0_r_def
    z(:) = 0.0_r_def
    dz = 2.3_r_def
    do k = 0,nlayers-1
      x(map_wx(2)+k) = dx
      x(map_wx(4)+k) = dx
      y(map_wx(3)+k) = dy
      y(map_wx(4)+k) = dy

      do df = 1,4
        x(map_wx(4+df)+k) = x(map_wx(df)+k)
        y(map_wx(4+df)+k) = y(map_wx(df)+k)
      end do
     ! Stretch the vertical mesh a (very) nonuniform way(
      dz = dz * 1.5_r_def
      do df = 1,4
        if ( k == 0 ) then
          z(map_wx(df)+k) = 0.0_r_def
        else
          z(map_wx(df)+k) = z(map_wx(4+df)+k-1)
        end if
        z(map_wx(4+df)+k) = z(map_wx(df)+k) + dz
      end do
      mdw3(map_w3(1)+k) = dx*dy*dz

      z_w3 = 0.5_r_def*(z(map_wx(1)+k)+z(map_wx(5)+k))
      density(1+k) = this%test_function(z_w3)
    end do

    ! Compute the coeffs for the stretched mesh
    call poly1d_vert_flux_coeffs_code(nlayers,                   &
                                      coeff,                     &
                                      mdw3,                      &
                                      x, y, z,                   &
                                      ndf_w3,                    &
                                      undf_w3,                   &
                                      map_w3,                    &
                                      ndf_wx,                    &
                                      undf_wx,                   &
                                      map_wx,                    &
                                      basis_wx,                  &
                                      face_basis_wx,             &
                                      global_order,              &
                                      nfaces_v,                  &
                                      nqp_h, nqp_v, wqp_h, wqp_v,&
                                      nfaces_v, nqp_f, wqp_f )

    ! The best way to check the coefficients is to call the kernel
    ! to reconstruc the field, this will compute flux =
    ! wind*reconstructed(density) and so we can then easily check
    ! the reconstructed field with the exact answer.
    ! this is done for both positive and negative winds
    ! to check the upwind reconstruction in both directions
    do df = 1,4
      map_w2(df) = 1 + (df-1)*nlayers
    end do
    map_w2(5) = 1 + 4*nlayers
    map_w2(6) = map_w2(5) + 1

    do i = 0,1
      wind = (-1.0_r_def + 2.0_r_def*real(i,r_def))*dx*dy
      flux = 0.0_r_def

      call poly1d_vert_flux_code( nlayers,              &
                                 flux,                 &
                                 wind,                 &
                                 density,              &
                                 coeff,                &
                                 ndf_w2,               &
                                 undf_w2,              &
                                 map_w2,               &
                                 basis,                &
                                 ndf_w3,               &
                                 undf_w3,              &
                                 map_w3,               &
                                 global_order,         &
                                 nfaces_v,             &
                                 outward_normals,      &
                                 .false. )

      if ( r_def == real64 ) then
        use_tol = tol
      else
        mx = 0.0_r_def
        do k = 0, nlayers
           mx = max( mx, flux(map_w2(5)+k)/wind(map_w2(5)+k) )
        end do
        use_tol = max(tol, 10.0_r_def*spacing( mx ))
      end if
      do k = 0,nlayers-1
        div_f = (flux(map_w2(6)+k)-flux(map_w2(5)+k))/mdw3(map_w3(1)+k)
        ans = this%test_function(z(map_wx(1)+k))
        @assertEqual(ans, flux(map_w2(5)+k)/wind(map_w2(5)+k), use_tol)
      end do
      k = nlayers
      ans = this%test_function(z(map_wx(5)+k-1))
      @assertEqual(ans, flux(map_w2(5)+k)/wind(map_w2(5)+k), use_tol)
    end do

  end subroutine test_stretched_mesh

  subroutine tearDown( this )
    implicit none

    class(poly1d_vert_flux_coeffs_test_type), intent(inout) :: this
    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()
  end subroutine tearDown

  pure function test_function(this, z) result(fz)

    implicit none

    class(poly1d_vert_flux_coeffs_test_type), intent(in) :: this
    real(r_def), intent(in) :: z
    real(r_def)             :: fz

    fz = z

  end function test_function

end module poly1d_vert_flux_coeffs_kernel_mod_test
