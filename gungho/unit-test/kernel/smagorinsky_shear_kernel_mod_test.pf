!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

module smagorinsky_shear_kernel_mod_test


  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size, &
                                                  get_w2_m3x3_q3x3x3_size, &
                                                  get_w3_m3x3_q3x3x3_size, &
                                                  get_wtheta_m3x3_q3x3x3_size
  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap,      &
                                                  get_w2_m3x3_dofmap,      &
                                                  get_w3_m3x3_dofmap,      &
                                                  get_wtheta_m3x3_dofmap,  &
                                                  get_m3x3_stencil_dofmap_cross
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( )

    use smagorinsky_shear_kernel_mod, only : smagorinsky_shear_code

    implicit none

    integer(i_def) :: nlayers, ncells
    integer(i_def) :: ndf_w0, undf_w0, ndf_w2, undf_w2, &
                      ndf_w3, undf_w3, ndf_wt, undf_wt
    integer(i_def) :: dim_space, dim_space_diff
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_w2(:,:)
    integer(i_def), allocatable :: map_w3(:,:)
    integer(i_def), allocatable :: map_wt(:,:)
    integer(i_def), allocatable :: stencil_map_w2(:,:,:)
    integer(i_def), allocatable :: stencil_map_w3(:,:,:)
    integer(i_def), allocatable :: stencil_map_wt(:,:,:)

    real(r_def), allocatable :: u(:)
    real(r_def), allocatable :: shear(:)
    real(r_def), allocatable :: height_wth(:)
    real(r_def), allocatable :: height_w3(:)
    real(r_def), allocatable :: chi1(:), chi2(:), chi3(:)

    real(r_def), parameter :: tol = 1.0e-10_r_def
    real(r_def), parameter :: dx = 1000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def) :: answer

    integer(i_def) :: icell, i, j, k

    integer(i_def) :: cell


    ! Variables for testing smagorinsky_shear
    ! Get sizes of everything
    nlayers=3
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w2_m3x3_q3x3x3_size( ndf_w2, undf_w2, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wt, undf_wt, ncells, &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v, &
                                  nlayers )
    ! Setup maps
    call get_w0_m3x3_dofmap(map_w0)
    call get_w2_m3x3_dofmap(map_w2)
    call get_m3x3_stencil_dofmap_cross(stencil_map_w2, map_w2)
    call get_w3_m3x3_dofmap(map_w3)
    call get_m3x3_stencil_dofmap_cross(stencil_map_w3, map_w3)
    call get_wtheta_m3x3_dofmap(map_wt)
    call get_m3x3_stencil_dofmap_cross(stencil_map_wt, map_wt)

    ! Compute coordinates
    allocate(chi1(undf_w0))
    allocate(chi2(undf_w0))
    allocate(chi3(undf_w0))
    allocate(height_wth(undf_wt))
    allocate(height_w3(undf_w3))
    icell = 1
    do j = 1,3
      do i = 1,3
        do k = 0,3
          chi1((icell-1)*4+k+1) = real(i-1)*dx
          chi2((icell-1)*4+k+1) = real(j-1)*dy
          chi3((icell-1)*4+k+1) = real(k)*dz
          height_wth((icell-1)*4+k+1) = real(k)*dz
        end do
        do k = 0,2
          height_w3((icell-1)*3+k+1) = (real(k)+0.5_r_def)*dz
        end do
        icell = icell + 1
      end do
    end do

    ! Create the data
    cell = 5
    allocate(u(undf_w2))
    allocate(shear(undf_wt))
    u(:) = 1.0_r_def
    shear(:) = 0.0_r_def

    u(stencil_map_w2(5,1,cell)+0) = 0.0_r_def
    u(stencil_map_w2(5,1,cell)+1) = 1.0_r_def
    u(stencil_map_w2(5,1,cell)+2) = 2.0_r_def
    u(stencil_map_w2(5,1,cell)+3) = 3.0_r_def

    ! Call the kernel
    call smagorinsky_shear_code( nlayers,                               &
                                 shear,                                 &
                                 u,                                     &
                                 5, stencil_map_w2(:,:,cell),           &
                                 height_wth,                            &
                                 5, stencil_map_wt(:,:,cell),           &
                                 height_w3,                             &
                                 5, stencil_map_w3(:,:,cell),           &
                                 chi1, chi2, chi3,                      &
                                 ndf_wt, undf_wt, map_wt(:,cell),       &
                                 ndf_w2, undf_w2, map_w2(:,cell),       &
                                 ndf_w3, undf_w3, map_w3(:,cell),       &
                                 ndf_w0, undf_w0, map_w0(:,cell)        &
                               )

     answer = 0.7071067811e-3_r_def
     k = 1
     @assertEqual(answer, shear(map_wt(1,cell) + k), tol)

     deallocate(map_w0)
     deallocate(map_w2)
     deallocate(stencil_map_w2)
     deallocate(map_w3)
     deallocate(stencil_map_w3)
     deallocate(map_wt)
     deallocate(stencil_map_wt)
     deallocate(chi1)
     deallocate(chi2)
     deallocate(chi3)
     deallocate(height_wth)
     deallocate(height_w3)
     deallocate(u)
     deallocate(shear)

  end subroutine test_all

end module smagorinsky_shear_kernel_mod_test
