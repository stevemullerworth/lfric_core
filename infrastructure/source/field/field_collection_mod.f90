!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!
!> @brief Holds and manages fields in a collection
!>
!> @details A container that holds a collection of fields. Fields that are
!>          presented to the field_collection through the add_field() method are
!>          copied, so when the original goes out of scope, the copy in the
!>          field_collection will continue to be maintained.
!
module field_collection_mod

  use constants_mod,           only: i_def, l_def, str_def
  use field_mod,               only: field_type, &
                                     field_pointer_type
  use integer_field_mod,       only: integer_field_type, &
                                     integer_field_pointer_type
  use r_solver_field_mod,      only: r_solver_field_type, &
                                     r_solver_field_pointer_type
  use r_tran_field_mod,        only: r_tran_field_type, &
                                     r_tran_field_pointer_type
  use pure_abstract_field_mod, only: pure_abstract_field_type
  use log_mod,                 only: log_event, log_scratch_space, &
                                     LOG_LEVEL_ERROR
  use linked_list_data_mod,    only: linked_list_data_type
  use linked_list_mod,         only: linked_list_type, &
                                     linked_list_item_type

  implicit none

  private

  !-----------------------------------------------------------------------------
  ! Type that holds a collection of fields in a linked list
  !-----------------------------------------------------------------------------
  type, extends(linked_list_data_type), public :: field_collection_type

    private
    !> The name of the field collection if provided.
    character(str_def)     :: name = 'unnamed_collection'

    !> A linked list of the fields contained within the collection
    type(linked_list_type) :: field_list

  contains
    procedure, public :: initialise
    procedure, public :: copy_collection
    procedure, public :: add_field
    procedure, public :: add_reference_to_field
    procedure, public :: remove_field
    procedure, public :: field_exists
    procedure, public :: get_first_item
    procedure, public :: get_field
    procedure, public :: get_integer_field
    procedure, public :: get_r_solver_field
    procedure, public :: get_r_tran_field
    procedure, public :: get_length
    procedure, public :: get_name
    procedure, public :: clear

    procedure, private :: collection_copy_constructor

    generic, public   :: assignment(=) => collection_copy_constructor
    final             :: field_collection_destructor
  end type field_collection_type

contains

!> Initialises a field collection
!> @param [in] name The name given to the collection
subroutine initialise(self, name)

  implicit none

  class(field_collection_type), intent(inout) :: self
  character(*),       optional, intent(in)    :: name

  self%field_list = linked_list_type()
  if(present(name))self%name = trim(name)

end subroutine initialise

!> Adds a field to the collection. The field maintained in the collection will
!> either be a copy of the original or a field pointer object containing a
!> pointer to a field held elsewhere..
!> @param [in] field The field that is to be copied into the collection or a
!>                   field pointer object that is to be stored in the collection
subroutine add_field(self, field)

  implicit none

  class(field_collection_type), intent(inout) :: self
  class(pure_abstract_field_type), intent(in) :: field

  ! Check field name is valid, if not then exit with error
  select type(infield => field)
    type is (field_type)
      if ( trim(infield%get_name()) == 'none' .OR. &
                                  trim(infield%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (integer_field_type)
      if ( trim(infield%get_name()) == 'none' .OR. &
                                  trim(infield%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (r_solver_field_type)
      if ( trim(infield%get_name()) == 'none' .OR. &
                                  trim(infield%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (r_tran_field_type)
      if ( trim(infield%get_name()) == 'none' .OR. &
                                  trim(infield%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (field_pointer_type)
      if ( trim(infield%field_ptr%get_name()) == 'none' .OR. &
                                  trim(infield%field_ptr%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%field_ptr%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (integer_field_pointer_type)
      if ( trim(infield%field_ptr%get_name()) == 'none' .OR. &
                                  trim(infield%field_ptr%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%field_ptr%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
     type is (r_solver_field_pointer_type)
      if ( trim(infield%field_ptr%get_name()) == 'none' .OR. &
                                  trim(infield%field_ptr%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%field_ptr%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
     type is (r_tran_field_pointer_type)
      if ( trim(infield%field_ptr%get_name()) == 'none' .OR. &
                                  trim(infield%field_ptr%get_name()) == 'unset') then
        write(log_scratch_space, '(3A)') &
        'Field name [', trim(infield%field_ptr%get_name()), &
        '] is an invalid field name, please choose a unique field name.'
        call log_event(log_scratch_space, LOG_LEVEL_ERROR)
      end if
  end select

  ! Check if field exists in collection already, if it does, exit with error
  select type(infield => field)
    type is (field_type)
      if ( self%field_exists( trim(infield%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (integer_field_type)
      if ( self%field_exists( trim(infield%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (r_solver_field_type)
      if ( self%field_exists( trim(infield%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (r_tran_field_type)
      if ( self%field_exists( trim(infield%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (field_pointer_type)
      if ( self%field_exists( trim(infield%field_ptr%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%field_ptr%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (integer_field_pointer_type)
      if ( self%field_exists( trim(infield%field_ptr%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%field_ptr%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (r_solver_field_pointer_type)
      if ( self%field_exists( trim(infield%field_ptr%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%field_ptr%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
    type is (r_tran_field_pointer_type)
      if ( self%field_exists( trim(infield%field_ptr%get_name()) ) ) then
        write(log_scratch_space, '(4A)') &
          'Field [', trim(infield%field_ptr%get_name()), &
          '] already exists in field collection: ', trim(self%name)
        call log_event( log_scratch_space, LOG_LEVEL_ERROR)
      end if
  end select

  ! Finished checking - so the field must be good to add - so add it
  call self%field_list%insert_item( field )

end subroutine add_field

!> Check if a field is present the collection
!> @param [in] field_name The name of the field to be checked
!> @return exists Flag stating if field is present or not
function field_exists(self, field_name) result(exists)

  implicit none

  class(field_collection_type), intent(in) :: self

  character(*), intent(in) :: field_name
  logical(l_def)           :: exists

  ! Pointer to linked list - used for looping through the list
  type(linked_list_item_type), pointer :: loop => null()

  ! start at the head of the mesh collection linked list
  loop => self%field_list%get_head()

  do
    ! If list is empty or we're at the end of list and we didn't find the
    ! field, set 'exists' to be false
    if ( .not. associated(loop) ) then
      exists=.false.
      exit
    end if
    ! otherwise search list for the name of field we want

    ! 'cast' to the field_type
    select type(listfield => loop%payload)
      type is (field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (integer_field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (integer_field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (r_solver_field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (r_solver_field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (r_tran_field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          exists=.true.
          exit
      end if
      type is (r_tran_field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          exists=.true.
          exit
      end if
    end select

    loop => loop%next
  end do

end function field_exists

!> Adds a pointer to a field into the field collection. The pointer will point
!> to a field held elsewhere. If that field is destroyed, the pointer will
!> become an orphan.
!> @param [in] field_ptr : A pointer to the field (real, integer, r_solver or r_tran)
!>                         that is to be referenced in the collection.
! The routine accepts a pointer to a field (real, integer, r_solver or r_tran). It
! packages it up into a field pointer object and calls the routine to add this
! to the collection
subroutine add_reference_to_field(self, field_ptr)

  implicit none

  class(field_collection_type), intent(inout)          :: self
  class(pure_abstract_field_type), pointer, intent(in) :: field_ptr

  type(field_type), pointer :: fld_ptr
  type(field_pointer_type)  :: field_pointer
  type(integer_field_type), pointer  :: int_fld_ptr
  type(integer_field_pointer_type)   :: integer_field_pointer
  type(r_solver_field_type), pointer :: r_solver_fld_ptr
  type(r_solver_field_pointer_type)  :: r_solver_field_pointer
  type(r_tran_field_type), pointer   :: r_tran_fld_ptr
  type(r_tran_field_pointer_type)    :: r_tran_field_pointer

  select type(field_ptr)
    type is (field_type)
      ! Create a field pointer object that just contains a field pointer
      fld_ptr => field_ptr
      call field_pointer%field_pointer_initialiser(fld_ptr)
      call self%add_field( field_pointer )
    type is (integer_field_type)
      ! Create an integer field pointer object that just contains a field ptr
      int_fld_ptr => field_ptr
      call integer_field_pointer%integer_field_pointer_initialiser(int_fld_ptr)
      call self%add_field( integer_field_pointer )
    type is (r_solver_field_type)
      ! Create an r_solver field pointer object that just contains a field ptr
      r_solver_fld_ptr => field_ptr
      call r_solver_field_pointer%r_solver_field_pointer_initialiser(r_solver_fld_ptr)
      call self%add_field( r_solver_field_pointer )
    type is (r_tran_field_type)
      ! Create an r_tran field pointer object that just contains a field ptr
      r_tran_fld_ptr => field_ptr
      call r_tran_field_pointer%r_tran_field_pointer_initialiser(r_tran_fld_ptr)
      call self%add_field( r_tran_field_pointer )
  end select

end subroutine add_reference_to_field

!> Remove a field from the collection
!> @param [in] field_name The name of the field to be removed
subroutine remove_field(self, field_name)

  implicit none

  class(field_collection_type), intent(inout) :: self
  character(*), intent(in) :: field_name

  ! Pointer to linked list - used for looping through the list
  type(linked_list_item_type), pointer :: loop => null()

  ! start at the head of the mesh collection linked list
  loop => self%field_list%get_head()

  do
    ! If list is empty or we're at the end of list and we didn't find the
    ! field, fail with an error
    if ( .not. associated(loop) ) then
      write(log_scratch_space, '(4A)') 'Cannot remove field. No field [', &
         trim(field_name), '] in field collection: ', trim(self%name)
      call log_event( log_scratch_space, LOG_LEVEL_ERROR)
    end if
    ! otherwise search list for the name of field we want

    ! 'cast' to the field_type
    select type(listfield => loop%payload)
      type is (field_type)
        if ( trim(field_name) == trim(listfield%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (integer_field_type)
        if ( trim(field_name) == trim(listfield%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (r_solver_field_type)
        if ( trim(field_name) == trim(listfield%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (r_tran_field_type)
        if ( trim(field_name) == trim(listfield%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (field_pointer_type)
        if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (integer_field_pointer_type)
        if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (r_solver_field_pointer_type)
        if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
      type is (r_tran_field_pointer_type)
        if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          call self%field_list%remove_item(loop)
          exit
        end if
    end select

    loop => loop%next
  end do

end subroutine remove_field

!> Access the first item from the collection
!> @return item Pointer to the first item in the collection
function get_first_item(self) result(item)

  implicit none

  class(field_collection_type), intent(in) :: self

  type(linked_list_item_type), pointer :: item

  item => self%field_list%get_head()

end function get_first_item

!> Access a field from the collection
!> @param [in] field_name The name of the field to be accessed
!> @return field Pointer to the field that is extracted
function get_field(self, field_name) result(field)

  implicit none

  class(field_collection_type), intent(in) :: self

  character(*), intent(in) :: field_name
  type(field_type), pointer :: field

  ! Pointer to linked list - used for looping through the list
  type(linked_list_item_type), pointer :: loop => null()

  ! start at the head of the mesh collection linked list
  loop => self%field_list%get_head()

  do
    ! If list is empty or we're at the end of list and we didn't find the
    ! field, fail with an error
    if ( .not. associated(loop) ) then
      write(log_scratch_space, '(4A)') 'No field [', trim(field_name), &
         '] in field collection: ', trim(self%name)
      call log_event( log_scratch_space, LOG_LEVEL_ERROR)
    end if
    ! otherwise search list for the name of field we want

    ! 'cast' to the field_type
    select type(listfield => loop%payload)
      type is (field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          field => listfield
          exit
      end if
      type is (field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          field => listfield%field_ptr
          exit
      end if
    end select

    loop => loop%next
  end do

end function get_field

!> Access an integer field from the collection
!> @param [in] field_name The name of the intager field to be accessed
!> @return field Pointer to the integer field that is extracted
function get_integer_field(self, field_name) result(field)

  implicit none

  class(field_collection_type), intent(in) :: self

  character(*), intent(in) :: field_name
  type(integer_field_type), pointer :: field

  ! Pointer to linked list - used for looping through the list
  type(linked_list_item_type), pointer :: loop => null()

  ! start at the head of the mesh collection linked list
  loop => self%field_list%get_head()

  do
    ! If list is empty or we're at the end of list and we didn't find the
    ! field, fail with an error
    if ( .not. associated(loop) ) then
      write(log_scratch_space, '(4A)') 'No integer field [', trim(field_name), &
         '] in field collection: ', trim(self%name)
      call log_event( log_scratch_space, LOG_LEVEL_ERROR)
    end if
    ! otherwise search list for the name of field we want

    ! 'cast' to the integer_field_type
    select type(listfield => loop%payload)
      type is (integer_field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          field => listfield
          exit
      end if
      type is (integer_field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          field => listfield%field_ptr
          exit
      end if
    end select

    loop => loop%next
  end do

end function get_integer_field

!> Access an r_solver field from the collection
!> @param [in] field_name The name of the intager field to be accessed
!> @return field Pointer to the r_solver field that is extracted
function get_r_solver_field(self, field_name) result(field)

  implicit none

  class(field_collection_type), intent(in) :: self

  character(*), intent(in) :: field_name
  type(r_solver_field_type), pointer :: field

  ! Pointer to linked list - used for looping through the list
  type(linked_list_item_type), pointer :: loop => null()

  ! start at the head of the mesh collection linked list
  loop => self%field_list%get_head()

  do
    ! If list is empty or we're at the end of list and we didn't find the
    ! field, fail with an error
    if ( .not. associated(loop) ) then
      write(log_scratch_space, '(4A)') 'No r_solver field [', trim(field_name), &
         '] in field collection: ', trim(self%name)
      call log_event( log_scratch_space, LOG_LEVEL_ERROR)
    end if
    ! otherwise search list for the name of field we want

    ! 'cast' to the r_solver_field_type
    select type(listfield => loop%payload)
      type is (r_solver_field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          field => listfield
          exit
      end if
      type is (r_solver_field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          field => listfield%field_ptr
          exit
      end if
    end select

    loop => loop%next
  end do

end function get_r_solver_field

!> Access an r_tran field from the collection
!> @param [in] field_name The name of the intager field to be accessed
!> @return field Pointer to the r_tran field that is extracted
function get_r_tran_field(self, field_name) result(field)

  implicit none

  class(field_collection_type), intent(in) :: self

  character(*), intent(in) :: field_name
  type(r_tran_field_type), pointer :: field

  ! Pointer to linked list - used for looping through the list
  type(linked_list_item_type), pointer :: loop => null()

  ! start at the head of the mesh collection linked list
  loop => self%field_list%get_head()

  do
    ! If list is empty or we're at the end of list and we didn't find the
    ! field, fail with an error
    if ( .not. associated(loop) ) then
      write(log_scratch_space, '(4A)') 'No r_tran field [', trim(field_name), &
         '] in field collection: ', trim(self%name)
      call log_event( log_scratch_space, LOG_LEVEL_ERROR)
    end if
    ! otherwise search list for the name of field we want

    ! 'cast' to the r_tran_field_type
    select type(listfield => loop%payload)
      type is (r_tran_field_type)
      if ( trim(field_name) == trim(listfield%get_name()) ) then
          field => listfield
          exit
      end if
      type is (r_tran_field_pointer_type)
      if ( trim(field_name) == trim(listfield%field_ptr%get_name()) ) then
          field => listfield%field_ptr
          exit
      end if
    end select

    loop => loop%next
  end do

end function get_r_tran_field

!> Returns the size of the field collection
function get_length(self) result(length)

  implicit none

  class(field_collection_type), intent(in) :: self
  integer(kind=i_def) :: length

  length = self%field_list%get_length()

end function get_length

!> Returns the name of the field collection
function get_name(self) result(name)

  implicit none

  class(field_collection_type), intent(in) :: self
  character(str_def) :: name

  name = self%name

end function get_name

!> DEPRECATED: Assignment operator between field_collection_type pairs.
!> Currently, this routine generates a (hopefully) useful message, then
!> performs a double allocate to force an error stack trace (which should be
!> useful to the developer - tells them where they have called the deprecated
!> routine from).
!>
!> @param[out] self   field_type lhs
!> @param[in]  source field_type rhs
subroutine collection_copy_constructor(self, source)

  use log_mod,         only : log_event, &
                              log_scratch_space, &
                              LOG_LEVEL_INFO
  implicit none
  class(field_collection_type), intent(inout) :: self
  type(field_collection_type), intent(in) :: source
  integer(i_def), allocatable :: ialloc(:)

  write(log_scratch_space,'(A,A)')&
     '"field_collection2=field_collection1" syntax no longer supported. '// &
     'Use "call field_collection1%copy_collection(field_collection2)". '// &
     'Field collection: ', source%get_name()
  call log_event(log_scratch_space,LOG_LEVEL_INFO )
  allocate(ialloc(1))   ! allocate the same memory twice, to force
  allocate(ialloc(1))   ! an error and generate a stack trace

end subroutine collection_copy_constructor

!> Returns a copy of the field collection.
!! Fields held by the collection are copied (metadata & data).
!! However, field references will only be pointer copies, and
!! so will share [meta]data with the original field reference.
subroutine copy_collection(self, dest, name)

  implicit none
  class(field_collection_type), intent(in)  :: self
  type(field_collection_type),  intent(out) :: dest
  character(*),      optional,  intent(in)  :: name

  class(linked_list_item_type), pointer :: field_item => null()

  ! Make sure the destination field collection starts with no fields
  call dest%clear()

  ! Create a new field collection for the destination
  if (present(name)) then
    call dest%initialise(name)
  else
    call dest%initialise(self%get_name())
  end if

  ! Populate the new collection with all the items from the source
  if (self%get_length() > 0) then
    field_item => self%field_list%get_head()
    do while (associated(field_item))
      select type(item => field_item%payload)
        type is (field_type)
          call dest%add_field(item)
        type is (integer_field_type)
          call dest%add_field(item)
        type is (r_solver_field_type)
          call dest%add_field(item)
        type is (r_tran_field_type)
          call dest%add_field(item)
        type is (field_pointer_type)
          call dest%add_field(item)
        type is (integer_field_pointer_type)
          call dest%add_field(item)
        type is (r_solver_field_pointer_type)
          call dest%add_field(item)
        type is (r_tran_field_pointer_type)
          call dest%add_field(item)
      end select
      field_item => field_item%next
    end do
  end if

end subroutine copy_collection

!> Clears all items from the field collection linked list
subroutine clear(self)

  implicit none

  class(field_collection_type), intent(inout) :: self

  call self%field_list%clear()

  return
end subroutine clear

!> Destructor for the field collection
subroutine field_collection_destructor(self)

  implicit none

  type (field_collection_type), intent(inout) :: self

  call self%clear()

  return
end subroutine field_collection_destructor

end module field_collection_mod
